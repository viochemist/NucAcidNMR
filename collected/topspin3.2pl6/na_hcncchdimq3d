;na_hcncchdimq3d
;avance-version (13/08/29)
;MQ-HCNCCH-TOCSY
;3D sequence with
;   inverse correlation for triple resonance
;      via MQ and inept transfer steps
;      and DIPSI3 spinlock
;
;      F1(H1') -> F2(C1') -> F3(N1/9,t1)
;              -> F2(C1' -> Cn',t2) -> F1(H',t3)
;
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho (t2)
;using sensitivity improvement
;(use parameterset NA_HCNCCHDIMQ3D)
;
;W. Hu, L. Jiang & Y.Q. Gosser, 
;   J. Magn. Reson. 145, 147-151 (2000)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d11=30m"
"d12=20u"

"d4=1.56m"
"d23=18m"
"d3=0.95m"
"d24=0.78m"


"d0=3u"
"d10=3u"

"in0=inf1/2"
"in10=inf2/2"


"l1=(d15/(p9*217.30))"


"DELTA=d0*2+larger(p2,p8)"
"DELTA1=p3*2"
"DELTA2=d23/2-d4-larger(p14,p30)/2"
"DELTA3=d23/2-larger(p14,p30)/2"
"DELTA4=d23/2-p1*2/3.1416"
"DELTA5=d23/2-d4-p1*2/3.1416"
"DELTA6=d4*2-p1"
"DELTA7=d3+d10*2+p2+p22-p19-d16"
"DELTA8=d24-p19-d16"
"DELTA9=d4-p19-d16"
"DELTA10=p19+d16+4u"


"spoff3=0"
"spoff9=0"
"spoff13=0"


"cnst30=(cnst21+cnst23)/2"


aqseq 321


1 ze
  d11 pl12:f2
2 d11 do:f2 

3 d12 pl32:f1 pl2:f2
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1
  30u fq=cnst21(bf ppm):f2
  50u UNBLKGRAD

  (p1 ph1)
  d4*2
  DELTA1
  (p2 ph1)
  (p3 ph1):f2
  d4
  d23*0.5
  (p2 ph1)
  DELTA2
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA3
  (p2 ph1)
  d23*0.5 pl2:f2
  (p3 ph2):f2
  (p1 ph3)

  p16:gp3
  d16 pl3:f3

  (p21 ph4):f3
  d0 
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  d0
  (p22 ph1):f3
  DELTA pl2:f2
  (p21 ph1):f3

  p16:gp4
  d16

  (ralign (p1 ph1) (p3 ph8):f2 )
  DELTA4
  (p2 ph1)
  DELTA3
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA2
  (p2 ph1)
  DELTA5 pl2:f2 pl3:f3
  (p1 ph1)
  DELTA6
  (p3 ph1):f2

  4u
  30u fq=cnst30(bf ppm):f2
  4u pl15:f2

						;begin DIPSI3
4 (p9*2.722 ph11):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph11):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph11):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph11):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph11):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph11):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph11):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph11):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph11):f2
  (p9*4.389 ph12):f2

  (p9*2.722 ph12):f2
  (p9*4.389 ph11):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph11):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph11):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph11):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph11):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph11):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph11):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph11):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph11):f2

  (p9*2.722 ph12):f2
  (p9*4.389 ph11):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph11):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph11):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph11):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph11):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph11):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph11):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph11):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph11):f2

  (p9*2.722 ph11):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph11):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph11):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph11):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph11):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph11):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph11):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph11):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph11):f2
  (p9*4.389 ph12):f2
  lo to 4 times l1
						;end DIPSI3


  p16:gp5
  d16 pl2:f2

  (p3 ph5):f2
  d10
  (p22 ph1):f3
  d3
  (p2 ph1)
  d10
  (p4 ph1):f2
  p19:gp1*-1*EA
  d16
  DELTA7

  (center (p1 ph1) (p3 ph6):f2 )

  p19:gp6
  d16
  DELTA8
  (center (p2 ph1) (p4 ph1):f2 )
  DELTA8
  p19:gp6
  d16
  (center (p1 ph2) (p3 ph7):f2 )
  p19:gp6
  d16
  DELTA9
  (center (p2 ph1) (p4 ph1):f2 )
  DELTA9
  p19:gp6
  d16
  (p1 ph1)
  DELTA10
  (p2 ph1)
  p19:gp2
  d16 pl12:f2

  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph4, +90), caldel(d0, +in0))
     F2EA(calgrad(EA) & calph(ph7, +180), caldel(d10, +in10) & calph(ph5, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=1 1 1 1 3 3 3 3
ph4=0 0 2 2
ph5=1 3
ph6=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph7=1 1 1 1 1 1 1 1 3 3 3 3 3 3 3 3
ph8=0
ph11=1
ph12=3
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl15: f2 channel - power level for TOCSY-spinlock
;pl32: f1 channel - power level for low power presaturation
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p9 : f2 channel -  90 degree low power pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(C1'H1'))                                   [1.56 msec]
;d3 : 1/(4J(Cn'Hn'))                                   [0.95 msec]
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d15: TOCSY mixing time (CC)                           [12 msec]
;d16: delay for homospoil/gradient recovery
;d23: 1/(4J(C1'N))                                     [18 msec]
;d24: 1/(8J(Cn'Hn'))                                   [0.78 msec]
;cnst21: C1' chemical shift (offset, in ppm)           [90 ppm]
;cnst23: C2' chemical shift (offset, in ppm)           [72 ppm]
;cnst30: ( cnst21 + cnst22 ) / 2
;o2p: C1'/C2' chemical shift (cnst30)
;o3p: 160 ppm
;l1: loop for DIPSI cycle:
;       mixing time = ((p9*217.3) * l1)                [12 msec]
;inf1: 1/SW(N) = 2 * DW(N)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(N)) = DW(N)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6
;			  80 : 20.1 :  -40 :   60 :   20 :    3

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1%
;gpz3: -40%
;gpz4: 60%
;gpz5: 20%
;gpz6: 3%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcncchdimq3d,v 1.8.6.2 2013/08/30 09:44:52 ber Exp $

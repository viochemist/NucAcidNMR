;na_hpctcojr
;avance-version (12/01/11)
;ct-HP COSY
;2D P-31/H-1 correlation via inept transfer
;phase sensitive
;using constant time
;water suppression using 1-1 echo pulse sequence
;(use parameterset NA_HPCTCOJR)
;
;T. Carlomagno, M. Hennig & J.R. Williamson, 
;   J. Biomol. NMR 22, 65-81 (2002)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d21=1s/(cnst4*4)"

"p0=p1*4/3"


"d0=3u"
"d20=d21-p16-d16"

"in0=inf1/2"

"in20=in0"


#   ifdef LABEL_CN
"DELTA1=d21-p16-d16-d0-p8"
#   else
"DELTA1=d21-p16-d16-d0"
#   endif /*LABEL_CN*/


"l4=d1/(p0+5m)"


1 ze

#   ifdef LABEL_CN
  d11 pl12:f2
2 d11 do:f2
  d12 pl2:f2
#   else
2 d11
  d12
#   endif /*LABEL_CN*/

3 d12

4 (p0 ph1)
  5m
  lo to 4 times l4
  4u
  50u UNBLKGRAD

  p16:gp1
  d16

  (p21 ph3):f3
  d0 

#   ifdef LABEL_CN
  (p8:sp13 ph1):f2
#   else
#   endif /*LABEL_CN*/

  DELTA1
  p16:gp2
  d16
  (center (p2 ph5) (p22 ph1):f3 )
  p16:gp2
  d16
  d20
  (p21 ph4):f3

  p16:gp3
  d16
  4u BLKGRAD

#   ifdef LABEL_CN
  (p1 ph1) 
  d19 pl12:f2
  (p1 ph6) 
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2
#   else
  (p1 ph1) 
  d19
  (p1 ph6) 
  go=2 ph31
  d11 mc #0 to 2
#   endif /*LABEL_CN*/

     F1PH(calph(ph3, +90), caldel(d0, +in0) & caldel(d20, -in20))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=1 1 1 1 3 3 3 3
ph5=0 0 2 2
ph6=2
ph31=0 2 0 2 2 0 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p0 : f1 channel - 120 degree high power pulse
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse                          [1 msec]
;p21: f2 channel -  90 degree high power pulse
;p22: f2 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                            [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d20: decremented delay (2D) = d21-p16-d16
;d21: 1/(4J(PH))                                        [20 msec]
;cnst4: = J(PH)
;o2p: 77 ppm
;o3p: 0 ppm
;l4: loop for saturation sequence: = d1 / (p0 + 5m)
;inf1: 1/SW(P) = 2 * DW(P)
;in0: 1/(2 * SW(P)) = DW(P)
;nd0: 2
;in20: = in0
;ns: 8 * n
;ds: >= 32
;td1: number of experiments        td1 max = 2 * d20 / in20
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3
;                        -30 :   10 :   30

;for z-only gradients:
;gpz1: -30%
;gpz2: 10%
;gpz3: 30%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: na_hpctcojr,v 1.4.8.1 2012/01/31 17:56:34 ber Exp $

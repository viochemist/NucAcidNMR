;na_trhcnchmqspwg3d
;avance-version (13/08/29)
;TROSY-MQ-HCNCH
;3D sequence with
;   inverse correlation for triple resonance
;      via TROSY and inept transfer steps
;
;      F1(H1',t1) -> F2(C1') -> F3(N1/9,t2)
;                    -> F2(C6/8) -> F1(H6/8,t3)
;
;using twofold modulated shaped pulse
;phase sensitive (t1)
;phase sensitive (t2)
;using constant time in t2
;water suppression using watergate sequence
;(use parameterset NA_TRHCNCHMQSPWG3D)
;
;R. Fiala, J. Czernek & V. Sklenar,
;   J. Biomol. NMR 16, 291-302 (2000)
;(R. Fiala, F. Jiang & V. Sklenar, 
;   J. Biomol. NMR 12, 373-383 (1998) )
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"

"d4=1.6m"
"d22=18m"
"d23=17m"
"d25=16m"
"d27=1.25m"

"p29=300u"

"cnst0=bf1*(cnst16)-o1*1000000"
"d19=1s/(4*cnst0)"


"d0=3u"
"d10=d23"
"d30=d23"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"


"DELTA1=d4-d0-larger(p8,p22)/2-p19-d16"
"DELTA2=d4+d0+larger(p8,p22)/2-p3-p19-d16"
"DELTA3=d22/2-p12/2-p16-d16"
"DELTA4=d22/2-p12/2-p22/2-p16-d16"
"DELTA5=d4-p14/2-p22/2"
"DELTA6=d22/2-d4-p14/2-p12/2-p16-d16"
"DELTA7=d4*2-p1"
"DELTA8=d25-p16-d16"
"DELTA9=d25-d27-p16-d16-p2"
"DELTA10=d27-p29-d16-p27*2.385-d19*5"
"DELTA11=d27-p29-d16-p27*2.154-p0*0.231-d19*5-4u"


"spoff3=0"
"spoff9=0"
"spoff13=0"
"spoff23=bf1*(cnst17/1000000)-o1"
"spoff0=bf2*((cnst22-cnst21)/2000000)"


aqseq 312


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl2:f2 pl3:f3
  50u UNBLKGRAD
  30u fq=cnst21(bf ppm):f2
  
  (p1 ph3)
  p19:gp3
  d16
  DELTA1
  d0
  (center (p8:sp13 ph1):f2 (p22 ph1):f3 )
  d0
  (p2 ph1)
  DELTA2
  p19:gp3
  d16 pl2:f2

  (p3 ph4):f2
  p16:gp4
  d16
  DELTA3
  (p12:sp23 ph1)
  DELTA4
  p16:gp4
  d16
  (p22 ph1):f3
  DELTA5
  (p14:sp3 ph1):f2 
  p16:gp4
  d16
  DELTA6
  (p12:sp23 ph1)
  DELTA3
  p16:gp4
  d16
  (p1 ph5) 
  DELTA7 pl2:f2
  (p3 ph6):f2

  p16:gp5
  d16

  (p21 ph7):f3
  d10 
  (center (p23:sp0 ph1):f2 (p22 ph1):f3 )
  d30
  (p21 ph8):f3

  p16:gp6
  d16 fq=cnst22(bf ppm):f2
  4u pl2:f2

  (p3 ph9):f2
  p16:gp7
  d16
  DELTA8
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA9
  p16:gp7
  d16
  (p2 ph1) 
  d27 pl2:f2
  (ralign (p1 ph1) (p3 ph1):f2 )

  p29:gp8
  d16
  DELTA10 pl18:f1
  (center
   (p27*0.231 ph1 d19*2 p27*0.692 ph1 d19*2 p27*1.462 ph1 d19*2 p27*1.462 ph10 d19*2 p27*0.692 ph10 d19*2 p0*0.231 ph10) 
   (p8:sp13 ph1):f2 
  )
  DELTA11
  p29:gp8
  d16 pl12:f2 pl16:f3

  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
     F2PH(calph(ph7, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit
   

ph1=0 
ph3=0
ph4=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph5=1 1 3 3
ph6=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph7=0 2
ph8=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph9=0 0 0 0 2 2 2 2
ph10=2
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2
     0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl18: f1 channel - power level for 3-9-19-pulse (watergate)
;pl32: f1 channel - power level for low power presaturation
;sp0 : f2 channel - shaped pulse 180 degree (C6/8 and C1')
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;sp23: f1 channel - shaped pulse 180 degree (H1' off resonance)
;p0 : f1 channel -  90 degree pulse at pl18
;                      use for fine adjustment
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p12: f1 channel - 180 degree shaped pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p23: f2 channel - 180 degree shaped pulse (twofold modulated)
;p27: f1 channel -  90 degree pulse at pl18
;p29: gradient pulse 3                                 [300 usec]
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(C1'H1'))                                   [1.6 msec]
;d10: incremented delay (F2 in 3D) = d23
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression, max. on H6/8
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d22: 1/(4J(C1'N))                                     [18 msec]
;d23: 1/(4J(C1'N)) and 1/(4J(C6/8N))                   [17 msec]
;d25: 1/(4J(C6/8N))                                    [16 msec]
;d27: 1/(4J(C6/8H6/8))                                 [1.25 msec]
;d30: decremented delay (F2 in 3D) = d23
;cnst16: H6/8 chemical shift (offset, in ppm)
;cnst17: H1' chemical shift (offset, in ppm)
;cnst21: C1' chemical shift (offset, in ppm)           [90 ppm]
;cnst22: C6(CU)/C8(AG) chemical shift (offset, in ppm) [137 ppm]
;o3p: 157 ppm
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(N)) = DW(N)
;nd10: 2
;in30: = in10
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5 : gp 6 : gp 7 : gp 8
;			   7 :   -3 :  -50 :  -10 :    2 :   31

;for z-only gradients:
;gpz3: 7%
;gpz4: -3%
;gpz5: -50%
;gpz6: -10%
;gpz7: 2%
;gpz8: 31%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100
;gpnam7: SMSQ10.100
;gpnam8: SMSQ10.50


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p12 (sp23) use Reburp pulse (DeltaOmega * DeltaT = 5.814)
;   to cover 2.95ppm (H1')
;   (pulselength: 3.3ms at 600.13 MHz)
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p23 (sp0) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      twofold modulated to cover 8.8ppm at 90ppm (C1')
;         and 137ppm (C6/C8) with carrier at 113.5ppm
;      (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_trhcnchmqspwg3d,v 1.7.6.2 2013/08/30 09:44:52 ber Exp $

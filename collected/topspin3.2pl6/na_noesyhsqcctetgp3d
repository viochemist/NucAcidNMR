;na_noesyhsqcctetgp3d
;avance-version (12/01/11)
;NOESY-HSQC
;3D sequence with
;   homonuclear correlation via dipolar coupling 
;   dipolar coupling may be due to noe or chemical exchange.
;   HC correlation via double inept transfer
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho-TPPI (t2)
;using trim pulses in inept transfer
;using constant time in t2
;(use parameterset NA_NOESYHSQCCTETGP3D)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"d4=1s/(cnst2*4)"
"d11=30m"

#   ifdef LABEL_CN
"p22=p21*2"
#   else
#   endif /*LABEL_CN*/


"d0=3u"
"d10=3u"
"d30=d23-p16-d16"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"


"DELTA1=d4-larger(p2,p8)/2"
"DELTA3=d4-larger(p2,p8)/2-p16-d16-4u"

#   ifdef LABEL_CN
"DELTA=larger(p8,p22)+d0*2"
"DELTA2=d23-d10-larger(p2,p22)-4u"
#   else
"DELTA=p8+d0*2"
"DELTA2=d23-d10-p2-4u"
#   endif /*LABEL_CN*/


"TAU=d8-p16-d16"


aqseq 321


1 ze
  d11 pl12:f2
2 d11 do:f2
3 d1 
  (p1 ph7)
  DELTA
  (p2 ph8)
  d0

#   ifdef LABEL_CN
  (center (p8:sp13 ph1):f2 (p22 ph1):f3 )
#   else
  (p8:sp13 ph1):f2
#   endif /*LABEL_CN*/

  d0
  (p1 ph1):f1
  TAU UNBLKGRAD
  p16:gp3
  d16
  (p1 ph1):f1
  DELTA1 pl0:f2
  4u
  (center (p2 ph1) (p8:sp13 ph6):f2 )
  4u
  DELTA1 pl2:f2
  p28 ph1
  4u
  (p1 ph2) 

  (p3 ph3):f2
  d10 

#   ifdef LABEL_CN
  (center (p2 ph5) (p22 ph1):f3 )
#   else
  (p2 ph5)
#   endif /*LABEL_CN*/

  4u
  DELTA2
  (p4 ph9):f2
  d30
  p16:gp1*EA*-1
  d16
  (p3 ph4):f2

  (p1 ph1) 
  DELTA1 pl0:f2
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  p16:gp2
  d16
  DELTA3 pl12:f2
  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph7, +90) & calph(ph8, +90), caldel(d0, +in0)) 
     F2EA(calgrad(EA), caldel(d10, +in10) & caldel(d30, -in30) & calph(ph3, +180) & calph(ph6, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph5=0 0 2 2
ph6=0
ph7=0 0 0 0 2 2 2 2
ph8=1 1 1 1 3 3 3 3
ph9=0 0 1 1
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp13: f2 channel - shaped pulse 180 degree
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p8: f2 channel - 180 degree shaped pulse for inversion
;p16: homospoil/gradient pulse
;p22: f3 channel - 180 degree high power pulse
;p28: f1 channel - trim pulse                          [1 msec]
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(CH))
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d23: d23 = T ,2T (constant time period) = n/J(CC)     [8.8 msec]
;d30: decremented delay (F2 in 3D) = d23-p16-d16
;cnst2: = J(CH)
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in30: = in10
;ns: 8 * n
;ds: >= 16
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3
;                         80 : 20.1 :   70    for C-13

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1%
;gpz3: 70%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: na_noesyhsqcctetgp3d,v 1.5.6.1 2012/01/31 17:56:34 ber Exp $

;na_hcnmqgpphpr
;avance-version (13/01/23)
;MQ-HCN
;2D inverse correlation for triple resonance using multiple
;   inept transfer steps
;
;      F1(H6/8) -> F2(C6/8) -> F3(N1/9,t1) 
;                    -> F2(C6/8) -> F1(H6/8,t2)
;      and/or
;      F1(H1') -> F2(C1') -> F3(N1/9,t1) 
;                    -> F2(C1') -> F1(H1',t2)
;
;using shaped pulses on f2 and f3
;phase sensitive (t1)
;(use parameterset NA_HCNMQGPPHPR)
;
;J.P. Marino, J.L. Diener, P.B. Moore & C.Griesinger,
;   J. Am. Chem. Soc. 119, 7361 - 7366 (1997)
;V. Sklenar, T. Diekmann, S.E. Butcher & J.Feigon,
;   J. Magn. Res. 130, 119 - 124 (1998)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d13=4u"


"d0=3u"

"in0=inf1/2"


"DELTA=d0*2+larger(p2,p4)"
"DELTA1=d4-p16-d16-d13"
"DELTA2=d22/2-d13-d16-p16-p2"
"DELTA3=d22/2-d13-d16-p16-larger(p14,p30)/2"


"spoff3=0"
"spoff9=0"


1 ze 
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3
3 d12 pl9:f1
  d1 cw:f1
  d13 do:f1
  d12 pl1:f1 pl2:f2 pl3:f3
 
  (p1 ph1)
  DELTA1 UNBLKGRAD
  d13
  p16:gp1
  d16
  (center (p2 ph10) (p4 ph10):f2 )
  d13
  p16:gp1
  d16
  DELTA1 
  (p3 ph3):f2
 
  DELTA2
  d13
  p16:gp2
  d16
  (p2 ph10)
  d13
  p16:gp2
  d16
  DELTA3

  (center (p14:sp3 ph1):f2 (p30:sp9 ph4):f3 )
  
  DELTA3
  d13
  p16:gp2
  d16 
  (p2 ph10)
  d13
  p16:gp2
  d16
  DELTA2 pl2:f2 pl3:f3
  (center (p1 ph2) (p3 ph2):f2 )
 
  d13
  p16:gp3
  d16

  (p21 ph5):f3
  d0
  (center (p2 ph10) (p4 ph10):f2 )
  d0
  (p22 ph1):f3
  DELTA
  (p21 ph1):f3

  d13
  p16:gp4
  d16
  
  (center (p1 ph2) (p3 ph2):f2 )
  DELTA2
  p16:gp5
  d16
  (p2 ph10)
  d13
  p16:gp5
  d16 
  DELTA3
  
  (center (p14:sp3 ph1):f2 (p30:sp9 ph10):f3 )
 
  DELTA3
  d13
  p16:gp5
  d16 pl2:f2
  (p2 ph10)
  d13
  p16:gp5
  d16
  DELTA2 
 
  (p3 ph1):f2
  DELTA1
  d13
  p16:gp6
  d16
  (center (p2 ph10) (p4 ph10):f2 )
  d13
  p16:gp6
  d16 
  DELTA1 pl12:f2 pl16:f3
  4u  BLKGRAD                 
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph4, +90) & calph(ph5, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=1
ph3=0 0 2 2 
ph4=2   
ph5=0 2
ph10=2
ph31=0 2 2 0


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl9 : f1 channel - power level for presaturation
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;sp3: f2 channel - shaped pulse 180 degree (on resonance)
;sp9: f3 channel - shaped pulse 180 degree (on resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse				[1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse for inversion
;d0 : incremented delay (2D)				[3 usec]
;d1 : relaxation delay; 1-5 * T1		
;d4 : 1/(4J(HC)) for both:                              [1.4 msec]
;                for H1':                               [1.56 msec]
;                for H8':                               [1.25 msec]
;d11: delay for disk I/O                               	[30 msec]
;d12: delay for power switching                        	[20 usec] 
;d13: short delay                                      	[4 usec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(4J(CN))
;     C1':                                              [19 msec]
;     C6/C8:                                            [16 msec]
;     both                                              [17.5 msec]
;     and constant time delay T(C) = 1/(J(CC)) with J(CC)=40 Hz
;o2p: 90(C1') or 137(C6/C8) or 113.5 ppm(both)
;o3p: 157 ppm
;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(N)) =  DW(N)
;nd0: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6
;                          4 :    5 :   30 :  -50 :    8 :    3

;for z-only gradients:
;gpz1: 4%
;gpz2: 5%
;gpz3: 30%
;gpz4: -50%  
;gpz5: 8%
;gpz6: 3%

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100
;gpname4: SMSQ10.100
;gpname5: SMSQ10.100
;gpname6: SMSQ10.100


;Processing

;F2 reverse: true


;calculate pulselength according to:
;   
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   either one pulse to cover 22.8ppm (C1' or C6/C8 in separate 
;      experiments)
;   (pulselength: 1ms at 600.13 MHz)
;   or a twofold modulated pulse to cover 8.8ppm at 90ppm (C1') 
;      and 137ppm (C6/C8) (both in one experiment)
;   (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcnmqgpphpr,v 1.4.8.2 2013/02/05 10:12:18 ber Exp $

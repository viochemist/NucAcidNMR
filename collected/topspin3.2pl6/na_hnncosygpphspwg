;na_hnncosygpphspwg
;avance-version (12/01/11)
;HNN-COSY
;2D H-1/N-15 correlation via multiple inept transfer
;for direct observation of hydrogen bonds in nucleic acid
;   base pairs by internucleotide 2J(N,N) couplings
;
;   for UA:
;      F1(H3) -> F3(N3) -> F3(N1,t1) -> F3(N3) -> F1(H3,t2)
;
;   for GC:
;      F1(H1) -> F3(N1) -> F3(N3,t1) -> F3(N1) -> F1(H1,t2)
;
;phase sensitive
;water suppression using watergate sequence
;using shaped 180degree pulses on f3
;(use parameterset NA_HNNCOSYGPPHSPWG)
;
;(A.J. Dingley & S. Grzesiek,
;   J. Am. Chem. Soc. 120, 8293-8297 (1998) )
;(M. Hennig & J.R. Williamson, 
;   Nucleic Acids Res. 28, 1585-1593 (2000) )
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"d11=30m"
"d12=20u"

"d26=2.25m"


"d0=3u"

"in0=inf1/4"


"DELTA1=d26-p16-d16-p32/2-4u"
"DELTA2=d26-p16-d16-p11-12u"
"DELTA3=d27/2-p16-d16-p32/2-4u"

"TAU=p21*4/3.1416+4u"


"spoff14=0"


1 ze 
  d11 pl16:f3
2 d1 do:f3
3 d12 pl1:f1
  50u UNBLKGRAD

  (p1 ph1)
  4u
  p16:gp1
  d16
  DELTA1
  (center (p2 ph2) (p32:sp14 ph1):f3 )
  4u
  DELTA1
  p16:gp1
  d16 pl3:f3
  (p1 ph2) 

  4u
  (p29:sp11 ph6:r):f1
  4u
  p16:gp2
  d16 pl1:f1

  (p21 ph3):f3
  4u
  p16:gp4
  d16
  DELTA3
  (p32:sp14 ph8):f3
  4u
  DELTA3
  p16:gp4
  d16
  TAU
  p16:gp4
  d16
  DELTA3
  (p32:sp14 ph8):f3
  4u
  DELTA3
  p16:gp4
  d16 pl3:f3
  (p21 ph4):f3

  d0
  (p2 ph5) 
  d0
  (p4 ph1):f2
  d0
  (p2 ph5) 
  d0

  (p21 ph2):f3
  4u
  p16:gp5
  d16
  DELTA3
  (p32:sp14 ph1):f3
  4u
  DELTA3
  p16:gp5
  d16
  TAU
  p16:gp5
  d16
  DELTA3
  (p32:sp14 ph1):f3
  4u
  DELTA3
  p16:gp5
  d16 pl3:f3
  (p21 ph9):f3

  4u
  p16:gp2
  d16
  4u
  (p29:sp11 ph7:r):f1
  4u
  4u pl1:f1

  (p1 ph1) 
  4u
  p16:gp3
  d16 pl0:f1
  DELTA2
  (p11:sp1 ph6:r):f1
  4u
  4u pl1:f1
  (center (p2 ph1) (p32:sp14 ph1):f3 )
  4u
  (p11:sp1 ph6:r):f1
  4u
  DELTA2
  p16:gp3
  d16 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3
  d1 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90) & calph(ph4, +90) & calph(ph8, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=1
ph3=0 2
ph4=1 1 1 1 3 3 3 3
ph5=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph6=2
ph7=2
ph8=0
ph9=0 0 2 2
ph31=0 2 2 0


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp1 : f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp11: f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp14: f3 channel - shaped pulse 180 degree (adiabatic)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p11: f1 channel -  90 degree shaped pulse              [1 msec]
;p16: homospoil/gradient pulse
;p21: f3 channel -  90 degree high power pulse
;p29: f1 channel -  90 degree shaped pulse              [1 msec]
;p32: f3 channel - 180 degree shaped pulse for inversion (adiabatic)
;d0 : incremented delay (2D)                            [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d26 : 1/(4J(NH))                                       [2.25 msec]
;d27 : 1/(4J(NN))                                       [15 msec]
;cnst4: = J(NH)
;o2p: 152 ppm
;o3p: 160 ppm
;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/(4 * SW(N)) = (1/2) DW(N)
;nd0: 4
;ns: 16 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: States-TPPI (or TPPI)
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:          gp 1 : gp 2 : gp 3 : gp 4 : gp 5
;                               50 :   80 :   30 :    3 :    3

;for z-only gradients:
;gpz1: 50%
;gpz2: 80%
;gpz3: 30%
;gpz4: 3%
;gpz5: 3%


;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100



;$Id: na_hnncosygpphspwg,v 1.5.6.1 2012/01/31 17:56:34 ber Exp $

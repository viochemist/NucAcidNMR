;na_hcnhgpph19
;avance-version (12/01/11)
;HCNH
;2D inverse correlation for triple resonance using multiple
;   inept transfer steps
;
;   F1(H1',t1) -> F2(C1') -> F3(N) -> F1(H,t2)
;
;phase sensitive
;water suppression using watergate sequence
;with decoupling during acquisition
;(use parameterset NA_HCNHGPPH19)
;
;S .Tate, A. Ono & M. Kainosho, J. Am. Chem. Soc. 116,
;   5977 - 5978 (1994)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d13=4u"


"d0=3u"

"in0=inf1/2"


"DELTA=d4-d0*2-p22-p4"
"DELTA1=d22-d13-p16-d16-larger(p14,p22)/2-d4-p2"
"DELTA2=d22-d13-d16-p16-larger(p14,p22)/2"
"DELTA3=d26-p16-d16-p27*2.385-d19*5+p22/2"
"DELTA4=d26-p16-d16-p27*2.154-p0*0.231-d19*5+p22/2-8u"
"DELTA5=d19-p22/2"
"DELTA6=d22-d13-d16-p16-larger(p14,p22)/2-d26-p2"


"spoff3=0"


1 ze 
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3
3 d1 pl1:f1 pl3:f3
  (p1 ph4)
  d0
  (p22 ph1):f3
  DELTA pl2:f2
  (p4 ph1):f2
  d0
  (p2 ph1) 
  d4 UNBLKGRAD             
  p28 ph1
  d13
  (p1 ph2)

  d13
  p16:gp1
  d16
  
  (p3 ph3):f2
  d4
  (p2 ph1)
  DELTA1
  d13
  p16:gp2
  d16 pl0:f2 
  (center (p14:sp3 ph1):f2 (p22 ph1):f3 )
  d13
  p16:gp2
  d16 pl2:f2 
  DELTA2
  (p3 ph6):f2

  d13
  p16:gp3
  d16
  
  (p21 ph5):f3
  DELTA2
  d13 
  p16:gp4
  d16 pl0:f2
  (center (p14:sp3 ph1):f2 (p22 ph1):f3 )
  d13 
  p16:gp4
  d16 pl2:f2
  DELTA6
  (p2 ph1)
  d26
  (p21 ph1):f3

  d13
  p16:gp5
  d16
  
  (p1 ph1)
  DELTA3
  p16:gp6
  d16 pl18:f1
  p27*0.231 ph7
  d19*2
  p27*0.692 ph7
  d19*2
  p27*1.462 ph7
  DELTA5
  (p22 ph1):f3
  DELTA5
  p27*1.462 ph8
  d19*2
  p27*0.692 ph8
  d19*2
  p0*0.231 ph8
  4u
  p16:gp6
  d16
  DELTA4 pl12:f2 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph4, +90), caldel(d0, +in0))
exit 

  
ph1=0
ph2=1
ph3=0 2 
ph4=0
ph5=0 0 0 0 2 2 2 2
ph6=0 0 2 2               
ph7=0
ph8=2
ph31=0 2 2 0 2 0 0 2


;pl0 : 120db
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl18: f1 channel - power level for 3-9-19-pulse (watergate)
;sp3: f2 channel - shaped pulse 180 degree (C1' on resonance)
;p0 : f1 channel -  90 degree pulse at pl18
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse				[1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p27: f1 channel -  90 degree pulse at pl18
;p28: f1 channel - trim pulse				[1 msec]
;d0 : incremented delay (2D)				[3 usec]
;d1 : relaxation delay; 1-5 * T1		
;d4 : 1/(4J(H1'C1')) 					[1.56 msec]
;d11: delay for disk I/O                               	[30 msec]
;d13: short delay                                      	[4 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d22: 1/(4J(CN))
;        C1'	 				   	[18 msec]
;d26: 1/(4J(HN))					[15 msec]
;cnst2: = J(HC) for C1'
;o2p: 90 ppm
;o3p: 157 ppm
;inf1: 1/SW(H) = 2 * DW(H)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6
;                         50 :    5 :   30 :    3 :  -40 :   30

;for z-only gradients:
;gpz1: 50%
;gpz2: 5%
;gpz3: 30%
;gpz4: 3%   
;gpz5: -40% 
;gpz6: 30% 

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100
;gpname4: SMSQ10.100
;gpname5: SMSQ10.100
;gpname6: SMSQ10.100


;Processing

;F2 reverse: true


;calculate pulselength according to:
;   
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)




;$Id: na_hcnhgpph19,v 1.4.8.1 2012/01/31 17:56:34 ber Exp $

;na_hnncosygpphwg
;avance-version (12/01/11)
;2D H-1/N-15 correlation via double inept transfer
;for direct observation of hydrogen bonds in nucleic acid
;   base pairs by internucleotide 2J(N,N) couplings
;
;      F1(H) -> F3(N1) -> F3(N2,t1) -> F3(N1) -> F1(H,t2)
;
;phase sensitive
;with decoupling during acquisition
;(use parameterset NA_HNNCOSYGPPHWG)
;
;A.J. Dingley & S. Grzesiek, J. Am. Chem. Soc. 120,
;  8293 - 8297 (1998)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d13=4u"


"d0=3u"

"in0=inf1/2"


"DELTA=d0*2+p2"
"DELTA1=d23-p22/2"
"DELTA2=d26-p16-d16-p11-12u"


1 ze
  d11 pl16:f3 
2 d11 do:f3
3 d1 pl1:f1 pl3:f3
  (p1 ph1)
  d26 pl3:f3
  (center (p2 ph1) (p22 ph1):f3 )
  d26 UNBLKGRAD
  (p1 ph2) 

  d13
  p16:gp1
  d16

  (p21 ph3):f3
  DELTA1
  (p22 ph4):f3
  DELTA1
  (p21 ph6):f3

  d0
  (p2 ph1)
  d0
  (p22 ph1):f3
  DELTA

  (p21 ph2):f3
  DELTA1
  (p22 ph1):f3
  DELTA1
  (p21 ph5):f3

  d13
  p16:gp2
  d16 pl0:f1
  (p11:sp1 ph7:r):f1
  4u 
  4u pl1:f1

  (p1 ph1) 
  4u
  p16:gp3              
  d16
  DELTA2 pl0:f1
  (p11:sp1 ph7:r):f1
  4u
  4u pl1:f1
  (center (p2 ph1) (p22 ph1):f3 )
  4u pl0:f1
  (p11:sp1 ph7:r):f1
  4u
  p16:gp3              
  d16
  DELTA2 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3
  d11 do:f3 mc #0 to 2
     F1PH(calph(ph3, +90) & calph(ph4, +90) & calph(ph6, +90), caldel(d0, +in0))
 exit

  
ph1=0 
ph2=1
ph3=0 2
ph4=0
ph5=0 0 2 2 
ph6=1 1 1 1 3 3 3 3
ph7=2
ph31=0 2 2 0 


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp1: f1 channel - shaped pulse  90 degree
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p11: f1 channel -  90 degree shaped pulse		[1 ms]
;p16: homospoil/gradient pulse				[1 ms]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (2D)				[3 us]
;d1 : relaxation delay; 1-5 * T1		
;d11: delay for disk I/O                               	[30 ms]
;d13: short delay                                      	[4 us]
;d16: delay for homospoil/gradient recovery
;d23: 1/(4J(NN)) 				        [15 ms]
;d26: 1/(4J(HN))  (J(HN) = 93Hz)			[2.5 ms]
;     for correlation of H2 with N in Adenin 12.5ms	
;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(N)) =  DW(N)
;nd0: 2
;ns: 8 * n
;ds: >= 32 
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI)
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3
;                         50 :   60 :   70

;for z-only gradients:
;gpz1: 50%
;gpz2: 60%
;gpz3: 70%

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100



;$Id: na_hnncosygpphwg,v 1.5.6.1 2012/01/31 17:56:34 ber Exp $

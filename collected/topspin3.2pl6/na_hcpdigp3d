;na_hcpdigp3d
;avance-version (12/01/11)
;HCP-TOCSY
;3D sequence with
;   inverse correlation for triple resonance using multiple
;      inept transfer steps and 
;      CC-TOCSY using DIPSI3
;
;      F1(H) -> F2(C) -> F3(P,t1) -> F2(C -> C1,t2) -> F1(H1,t3)
;
;phase sensitive (t1)
;phase sensitive (t2)
;with decoupling during acquisition
;only for D2O solution
;(use parameterset NA_HCPDIGP3D)
;
;J.P. Marino, H. Schwalbe, C. Anklin, W. Bermel, D.M. Crothers 
;   & C. Griesinger, J. Biomol. NMR 5, 87 - 92 (1995)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"
"d22=1s/(cnst5*8)"


"d0=3u"
"d10=3u"

"in0=inf1/2"
"in10=inf2/2"


"l1=(d15/(p9*217.30))"


"DELTA=d22-p16-d16-p2-d4"
"DELTA1=d22-p16-d16-p22"
"DELTA2=d0*2+larger(p2,p4)"
"DELTA3=d4-d10*2-p2+d12*2+p10-larger(p22,p10)+4u"
"DELTA4=d22-p22"


"spoff25=bf2*(cnst23/1000000)-o2"


aqseq 321


1 ze 
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1

  (p1 ph1)
  d4 pl2:f2 pl3:f3
  (center (p2 ph1) (p4 ph1):f2 )
  d4 UNBLKGRAD             
  (p28 ph1)
  4u
  (p1 ph2)

  p16:gp1
  d16
  
  (p3 ph3):f2
  d4
  (p2 ph1)
  p16:gp2
  d16
  DELTA
  (p4 ph1):f2
  (p22 ph4):f3
  DELTA1
  p16:gp2
  d16
  (p3 ph1):f2
 
  p16:gp3
  d16

  (p21 ph5):f3
  d0
  (center (p2 ph1) (p4 ph1):f2 )
  d0
  (p22 ph1):f3
  DELTA2
  (p21 ph1):f3

  p16:gp4
  d16
  
  (p3 ph6):f2
  d22
  (p4 ph7):f2
  (p22 ph1):f3
  DELTA4 pl15:f2

  (p20 ph10):f2
						;begin DIPSI3
9 (p9*2.722 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph10):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph10):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph10):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph10):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.722 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph10):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph10):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph10):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph10):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph10):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph10):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph10):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph10):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph10):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph10):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.722 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph10):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph10):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph10):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph10):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph10):f2
  (p9*4.389 ph12):f2
  lo to 9 times l1
						;end DIPSI3
  4u
  d12 pl0:f2
  (p10:sp25 ph1):f2
  4u
  d12 pl2:f2
  (p4 ph1):f2
  d10 pl0:f2 
  (center (p10:sp25 ph1):f2 (p22 ph1):f3  )
  4u
  DELTA3 pl2:f2
  (p2 ph1)
  d10
  (p4 ph1):f2
  d4
  (p3 ph8):f2

  p16:gp5
  d16

  (p1 ph2)
  d4
  (center (p2 ph1) (p4 ph1):f2 )
  d4 pl12:f2 pl16:f3 BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph4, +90) & calph(ph5, +90), caldel(d0, +in0))
     F2PH(calph(ph8, +90), caldel(d10, +in10))
exit
   

ph1=0
ph2=1
ph3=0 0 0 0 2 2 2 2 
ph4=0   
ph5=0 2
ph6=0
ph7=0
ph8=0 0 2 2
ph10=0
ph12=2
ph29=0
ph31=0 2 2 0 2 0 0 2


;pl0 : 120
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl15: f2 channel - power level for TOCSY-spinlock
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp25: f2 channel - shaped pulse 180 degree  (C2' off resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p9 : f2 channel -  90 degree low power pulse
;p10: f2 channel - 180 degree shape pulse
;p16: homospoil/gradient pulse				[1 ms]
;p20: f2 channel - trim pulse                           [2 ms]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p28: f1 channel - trim pulse                           [1 ms]
;d0 : incremented delay (F1 in 3D)			[3 us]
;d1 : relaxation delay; 1-5 * T1		
;d4 : 1/(4J(HC))					[1.6 ms]
;d10: incremented delay (F2 in 3D)			[3 us]
;d11: delay for disk I/O                               	[30 ms]
;d12: delay for power switching                        	[20 us] 
;d15: TOCSY mixing time (CC)                            [11 msec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(4J(CP)) :  C4'-P                               [12.5 ms]
;                   C5'-P                               [25 ms]
;cnst2: = J(HC)
;cnst5: = J(CP)
;cnst23: C2' chemical shift (offset, in ppm)            [72 ppm]
;o2p: 77 ppm
;o3p: 0 ppm
;l1: loop for DIPSI cycle:
;       mixing time = ((p9*217.3) * l1) + (p20)         [13 msec]
;inf1: 1/SW(P) = 2 * DW(P)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(P)) =  DW(P)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2  
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:  	gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6
;			  35 :   25 :   50     45     80 : 20.1 

;for z-only gradients:
;gpz1: -50%
;gpz2: 10%
;gpz3: 30%
;gpz4: -45%  
;gpz5: -30%

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100
;gpname4: SMSQ10.100
;gpname5: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;   
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p10 (sp25) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 11.4ppm at 72 (C2')
;   (pulselength: 2.0ms at 600.13 MHz)



;$Id: na_hcpdigp3d,v 1.8.6.1 2012/01/31 17:56:34 ber Exp $

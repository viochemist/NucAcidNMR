;na_hcchecgp3d
;avance-version (12/01/11)
;HCCH-E.COSY 
;3D sequence with
;   inverse correlation using inept transfer and
;      E.COSY 
;
;      F1(H,t1) -> F2(C,t2) -> F2(C') -> F1(H',t3)
;
;phase sensitive (t1)
;phase sensitive (t2)
;(use parameterset NA_HCCHECGP3D)
;
;L.E. Kay, G.Y. Xu, A.U. Singer, D.R. Muhandiram & J. D. Forman-Kay
;   J. Magn. Reson. B 101, 333 - 337 (1993)
;H. Schwalbe, J.P. Marino, G.C. King, R. Wechselberger, W. Bermel &
;   C. Griesinger, J. Biomol. NMR 4, 631 - 644 (1994)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d2=1s/(cnst2*2)"
"d4=1s/(cnst2*4)"
"d11=30m"
"d13=4u"
"d22=3s/(cnst3*4)"
"d23=1s/(cnst3*8)"


"p16=1m"
"p19=5m"
"p29=4.4m"


"d0=3u"
"d10=3u"
"d30=d22/2"			

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"


"DELTA=d4-p4-p22-d0*2"
"DELTA1=d22/2-p22-d10"
"DELTA2=d23-d2-p1*2-d13"


aqseq 321


1 ze
  d11 pl12:f2 pl16:f3 
2 d11 do:f2 do:f3
3 d1 pl2:f2 pl3:f3
  (p1 ph3)
  d0
  (p22 ph1):f3
  DELTA 
  (p4 ph1):f2
  d0 
  (p2 ph1) 
  d4 UNBLKGRAD           
  (p1 ph7) 
  
  d13
  p16:gp1
  d16

  (p3 ph4):f2	
  d3
  (center (p2 ph1) (p4 ph1):f2 )
  d3
  (p3 ph2):f2

  d12 pl10:f1
  (p17 ph1)
  (p17*2 ph2)
  4u
  p19:gp2
  d16
  (p1 ph2)
  4u
  p29:gp2
  d16 pl2:f2

  (p3 ph6):f2
  d10
  (p22 ph1):f3
  DELTA1
  (p4 ph1):f2
  d30				
  (p3 ph1):f2
  d23				
  (p4 ph1):f2
  DELTA2	
  (p1 ph11)
  d13
  (p1 ph12)
  d2 
  (center (p2 ph1) (p3 ph5):f2 )
  d2 pl12:f2 pl16:f3 
  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph3, +90), caldel(d0, +in0))
     F2PH(calph(ph6, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit


ph1=0
ph2=1
ph3=0
ph4=0 2
ph5=0 0 2 2
ph6=1
ph7=1 1 1 1 3 3 3 3
ph11=(360) 45
ph12=(360) 180
ph31=0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl10: f1 channel - power level for TOCSY-spinlock (trim pulse)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse 1 			[1 ms]
;p17: f1 channel - trim pulse                           [2.5 msec]
;p19: gradient pulse 2                                  [5 ms]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p29: gradient pulse 3                                  [4.4 ms]
;d0 : incremented delay (F1 in 3D)			[3 us]
;d1 : relaxation delay; 1-5 * T1		
;d2 : 1/(2J(HC))                                        [3.1 ms]
;d3 : 1/(4J(HC)) for CH (RNA)
;     1/(8J(HC)) for CH2 (DNA)
;d4 : 1/(4J(HC))					[1.6 ms]
;d10: incremented delay (F2 in 3D)			[3 us]
;d11: delay for disk I/O                               	[30 ms]
;d13: short delay                                      	[4 us]
;d16: delay for homospoil/gradient recovery
;d22: constant time delay for 
;        all correlations:     3/(4J(CC))               [18.8 ms]
;        C1' and C5':          1/(2J(CC))               [12.5 ms]
;d23: 1/(8J(CC))                                        [3.1 ms]
;d30: decremented delay (F2 in 3D) = d22/2
;cnst2: = J(HC)
;cnst3: = J(CC)
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in30: = in10
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30  
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:  	gp 1 : gp 2  
;			  35 :   60   

;for z-only gradients:
;gpz1: 35%
;gpz2: 60%

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100



;$Id: na_hcchecgp3d,v 1.2.8.1 2012/01/31 17:56:34 ber Exp $

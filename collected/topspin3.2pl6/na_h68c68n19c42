;na_h68c68n19c42
;avance-version (12/01/11)
;H6/8C6/8N1/9C2/4
;2D sequence with
;   inverse correlation for triple resonance
;      via inept transfer steps
;
;      F1(H6) -> F2(C6) -> F3(N1) -> F2(C2,t1)
;                   -> F3(N1) -> F2(C6) -> F1(H6,t2)
;      or
;      F1(H8) -> F2(C8) -> F3(N9) -> F2(C4,t1)
;                   -> F3(N9) -> F2(C8) -> F1(H8,t2)
;
;phase sensitive (t1)
;using sensitivity improvement
;(use parameterset NA_H68C68N19C42)
;
;B. Fuertig, C. Richter, W. Bermel & H. Schwalbe,
;   J. Biomol. NMR 28, 69-79 (2004)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"d11=30m"

"d4=1.25m"
"d23=15m"
"d25=15m"


"d0=3u"

"in0=inf1/2"


"DELTA=d0*4+p2*2+p30"
"DELTA1=d4-larger(p2,p8)/2-4u"
"DELTA2=d23-p16-d16"
"DELTA3=p16+d16+8u"


"spoff9=0"
"spoff10=0"
"spoff12=0"
"spoff13=0"
"spoff25=0"


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d1 pl3:f3

  30u fq=cnst22(bf ppm):f2
  
  (p1 ph1)
  DELTA1 pl0:f2
  4u
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  4u
  DELTA1 pl2:f2 UNBLKGRAD
  p28 ph1
  4u
  (p1 ph2) 

  p16:gp3
  d16

  (p3 ph3):f2
  d23
  4u
  (center (p10:sp25 ph1):f2 (p30:sp9 ph1):f3)
  4u
  d23 pl2:f2 pl3:f3
  (p3 ph2):f2

  p16:gp4
  d16 fq=cnst27(bf ppm):f2

  (p21 ph4):f3
  4u
  d25
  (center (p10:sp25 ph1):f2 (p30:sp9 ph1):f3 )
  d25
  4u pl3:f3
  (p21 ph2):f3

  (p15:sp10 ph5):f2
  d0 
  (p2 ph8) 
  d0 
  (p30:sp9 ph1):f3
  d0
  (p2 ph8) 
  d0 
  (p10:sp25 ph1):f2
  DELTA pl3:f3
  (p15:sp12 ph1):f2

  (p21 ph6):f3
  4u
  d25
  (center (p10:sp25 ph1):f2 (p30:sp9 ph1):f3 )
  d25
  4u pl3:f3
  (p21 ph2):f3

  30u fq=cnst22(bf ppm):f2
  4u pl2:f2

  (p3 ph7):f2
  d23
  (center (p10:sp25 ph1):f2 (p30:sp9 ph1):f3)
  DELTA2
  p16:gp1
  d16 pl2:f2

  (center (p1 ph1) (p3 ph8):f2 )
  d4
  (center (p2 ph1) (p4 ph1):f2 )
  d4
  (center (p1 ph2) (p3 ph9):f2 )
  4u
  DELTA1 pl0:f2
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  4u
  DELTA1
  (p1 ph1)
  DELTA3
  (p2 ph1)
  4u
  p16:gp2
  d16 pl12:f2 pl16:f3
  4u BLKGRAD

  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph5, +90), caldel(d0, +in0))
exit
   

ph1=0 
ph2=1
ph3=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph4=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph5=0 2
ph6=0 0 2 2
ph7=1
ph8=0 0 0 0 2 2 2 2
ph9=3 3 3 3 1 1 1 1
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2
  

;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;sp9 : f3 channel - shaped pulse 180 degree (N on resonance)
;sp10: f2 channel - shaped pulse  90 degree  (on resonance)
;sp12: f2 channel - shaped pulse  90 degree  (on resonance)
;                  for time reversed pulse
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;sp25: f2 channel - shaped pulse 180 degree (on resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p10: f2 channel - 180 degree shaped pulse
;p15: f2 channel -  90 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p28: f1 channel - trim pulse                          [1 ms]
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (2D)                           [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(CH))                                       [1.25 msec]
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d23: 1/(4J(C6/8N1/9))                                 [15 msec]
;d25: 1/(4J(N1/9C2/4))                                 [15 msec]
;cnst22: C6(CU)/C8(AG) chemical shift (offset, in ppm) [137 ppm]
;cnst27: C2/4 chemical shift (offset, in ppm)          [152 ppm]
;o2p: C2/4 chemical shift (cnst27)
;o3p: 160 ppm
;inf1: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(C)) = DW(C)
;nd0: 2
;ns: 16 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3 : gp 4
;                         80 : 20.1 :   50 :   30

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1%
;gpz3: 50%
;gpz4: 30%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p15 (sp10/sp12) use Q5/Q5tr pulse (DeltaOmega * DeltaT = 6.180)
;      to cover 20.5ppm on resonance
;      (pulselength: 2.0ms at 600.13 MHz)
;for p10 (sp25) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 11.4ppm on or off resonance
;      (pulselength: 2.0ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_h68c68n19c42,v 1.6.6.2 2012/01/31 17:56:34 ber Exp $

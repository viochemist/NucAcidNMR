;na_pcchco
;avance-version (12/01/11)
;CP-COSY, CH-COSY
;2D P-31/H-1 correlation via inept transfer steps
;   and hetero TOCSY using a DIPSI3 spinlock
;
;      F3(P,t1) -> F2(C) -> F1(H,t2)
;
;phase sensitive
;water suppression using jump-return pulse
;(use parameterset NA_PCCHCO)
;
;S.S. Wijmenga, H.A. Heus, H.A.E. Leeuw, H. Hoppe, 
;   M. van der Graaf & C.W. Hilbers, J. Biomol. NMR 5, 82-86 (1995)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"

"p0=p1*4/3"

"d22=12.5m"


"d0=3u"

"in0=inf1/2"


"DELTA=d22+d0*2+larger(p2,p4)-p4"
"DELTA1=d22+p22-d4-p2"
"DELTA2=d4-p16-d16-d19"


"l4=d1/(p0+5m)"


1 ze
  d11 pl12:f2

2 d11 do:f2
3 d12 pl1:f1 pl2:f2

4 (p0 ph1)
  5m
  lo to 4 times l4
  4u
  50u UNBLKGRAD

  p16:gp1
  d16

  (p21 ph3):f3
  d0 
  (center (p2 ph5) (p4 ph1):f2 )
  d0

  d22
  (p22 ph1):f3
  (p4 ph1):f2
  DELTA
  (p21 ph2):f3

  p16:gp2
  d16

  (p3 ph4):f2
  d22
  (p22 ph1):f3
  (p4 ph1):f2
  DELTA1
  (p2 ph1)
  d4
  (p3 ph1):f2

  p16:gp3
  d16
  4u BLKGRAD

  (p1 ph1)
  p16:gp4
  d16
  DELTA2
  (center (p1 ph1 d19*2 p1 ph6) (p4 ph1):f2 )
  DELTA2
  p16:gp4
  d16 pl12:f2

  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=2
ph31=0 2 0 2 2 0 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;p0 : f1 channel - 120 degree high power pulse
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse
;p21: f2 channel -  90 degree high power pulse
;p22: f2 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                            [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(CH))
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d22: 1/(4J(CP))                                        [12.5 msec]
;cnst2: = J(CH)
;o2p: 77 ppm
;o3p: 0 ppm
;l4: loop for saturation sequence: = d1 / (p0 + 5m)
;inf1: 1/SW(P) = 2 * DW(P)
;in0: 1/(2 * SW(P)) = DW(P)
;nd0: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 1 : gp 2 : gp 3 : gp 4
;                        -10 :  -60 :  -50 :   19

;for z-only gradients:
;gpz1: -10%
;gpz2: -60%
;gpz3: -50%
;gpz4: 19%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100



;$Id: na_pcchco,v 1.5 2012/01/31 17:49:27 ber Exp $

;na_hpcosyphpr
;avance-version (15/02/27)
;constant time HH-{31P}-COSY
;for determination of three-bond H3-P coupling constants
;   in nucleic acids
;with presaturation during relaxation delay
;phase sensitive
;
;G.M. Clore, E.C. Murphy, A.M. Gronenborn & A.Bax,
;   J. Magn. Res. 134, 164 - 167 (1998)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d13=4u"


"d22=25m"


"d0=3u"
"d20=d22+d0"

"in0=inf1"

"in20=in0"

"td1=tdmax(td1,d20*2,in20)"


"DELTA1=d22-p22"


"l0=1"


1 ze
2 d11 do:f3
3 d12 pl9:f1
  d1 cw:f1 ph29
  d13 do:f1
  d12 pl1:f1 pl3:f3
  p1 ph3
  d0

  if "l0 %2 == 1"
     {
     d22
     (center (p2 ph4) (p22 ph1):f3 )
     }
  else
     {
     (p22 ph1):f3
     DELTA1
     p2 ph4
     }

  d20 pl16:f3
  p1 ph1
  go=2 ph31 cpd3:f3
  d11 do:f3 mc #0 to 2 
     F1I(iu0, 2)
     F1PH(calph(ph3, +90) & calph(ph29, +90), caldel(d0, +in0) & caldel(d20, -in20))
exit


ph1=0
ph3=0 2
ph4=0 0 1 1 2 2 3 3
ph29=0
ph31=0 2 2 0 


;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl9 : f1 channel - power level for presaturation
;pl16: f3 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d13: short delay                                    [4 usec]
;d20: decremented delay (2D) = d22+d0
;d22: constant time delay T                          [25 msec]
;inf1: 1/SW(H) = 2 * DW(H)
;in0: 1/(1 * SW(H)) = 2 * DW(H)
;nd0: 1
;in20: = in0
;ns: 4 * n
;ds: 32
;td1: total number of experiments
;        = number of experiments for each 2D * 2
;FnMODE: States-TPPI (or TPPI)
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence



;$Id: na_hpcosyphpr,v 1.5.2.1 2015/03/03 11:21:24 ber Exp $

;na_hsqcf3gpnophxy
;avance-version (12/01/11)
;HSQC-NOESY
;2D H-1/P-31 correlation
;   using XY16-CPMG for transfer
;phase sensitive
;using f3 - channel
;(use parameterset NA_HSQCF3GPNOPHXY)
;
;B. Luy & J.P. Marino, J. Am. Chem. Soc. 123, 11306-11307 (2001)
;(F.A.A. Mulder, C.A.E.M. Spronk, M. Slijper, R. Kaptein &
;   R. Boelens, J. Biomol. NMR 8, 223-228 (1996))
;(Davis et al., J. Magn. Reson. 98, 207 - (1992))
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d26=1s/(cnst4*4)"


"d0=3u"

"in0=inf1/2"


#   ifdef LABEL_CN
"DELTA=d0*2+larger(p2,p8)"
#   else
"DELTA=d0*2+p2"
#   endif /*LABEL_CN*/

"TAU=d8-p16-d16"


"COUNTER=d26*2/((d21*2+larger(p2,p22))*16)"
"l4=COUNTER*16"

"d31=(d21*2+larger(p2,p22))*l4"


1 ze

#   ifdef LABEL_CN
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3
  d12 pl2:f2 pl3:f3
#   else
  d11 pl16:f3
2 d11 do:f3
  50u pl3:f3
#   endif /*LABEL_CN*/

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  50u pl1:f1 UNBLKGRAD
  20u rpp6
  20u rpp16

  (p1 ph1)

4 d21
  (center (p2 ph6) (p22 ph16):f3 )
  d21 ipp6 ipp16
  lo to 4 times l4

  (p1 ph2) 

  4u
  p16:gp3
  d16

  (p21 ph3):f3
  d0 

#   ifdef LABEL_CN
  (center (p2 ph5) (p8:sp13 ph1):f2 )
#   else
  (p2 ph5)
#   endif /*LABEL_CN*/

  d0
  (p22 ph4):f3
  DELTA
  (p21 ph4):f3

  4u
  p16:gp4
  d16

  (p1 ph1)

5 d21
  (center (p2 ph6) (p22 ph16):f3 )
  d21 ipp6 ipp16
  lo to 5 times l4

  (p1 ph2)
  d12 pl32:f1
  TAU cw:f1 ph29
  4u do:f1
  p16:gp5
  d16 pl1:f1
  (p1 ph1)

#   ifdef LABEL_CN
  d16 pl12:f2 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
#   else
  d16 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3
  d11 do:f3 mc #0 to 2 
#   endif /*LABEL_CN*/

     F1PH(calph(ph3, +90), caldel(d0, +in0))

  d31
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=0 1 0 1 1 0 1 0 2 3 2 3 3 2 3 2
ph16=0 1 0 1 1 0 1 0 2 3 2 3 3 2 3 2
ph29=0
ph31=0 2 0 2 2 0 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse                         [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                           [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d8 : mixing time
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d21: delay in XY16-CPMG                               [100 usec]
;d26: 1/(4J(PH))
;d31: 1/(2J(PH)) as executed
;cnst4: = J(PH)                                        [20 Hz]
;o2p: 77 ppm
;o3p: 0 ppm
;l4: loop for XY16-CPMG
;inf1: 1/SW(P) = 2 * DW(P)
;in0: 1/(2 * SW(P)) = DW(P)
;nd0: 2
;ns: 4 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 3 : gp 4 : gp 5
;                        -30 :  -20 :   70

;for z-only gradients:
;gpz3: -30%
;gpz4: -20%
;gpz5: 70%

;use gradient files:
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping



;$Id: na_hsqcf3gpnophxy,v 1.7.2.2 2015/09/23 17:04:13 ber Exp $

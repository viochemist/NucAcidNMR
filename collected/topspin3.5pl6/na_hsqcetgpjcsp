;na_hsqcetgpjcsp
;avance-version (12/01/11)
;2D H-1/X correlation via double inept transfer
;for determination of C4'H5' and C4'H5'' or C2'H1' coupling 
;   constants in RNA
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;using shaped pulses for inversion on f2 - channel
;
;J.P. Marino, H. Schwalbe, S.J. Glaser, C. Griesinger,
;   J. Am. Chem. Soc. 118, 4388 - 4395 (1996) 
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"
"d13=4u"
"d24=1s/(cnst2*cnst11)"


"d0=3u"

"in0=inf1/2"


"DELTA=p16+d16+d0*2+p2-4u"
"DELTA1=p16+d16+4u"


"spoff10=0"
"spoff12=0"
"spoff15=0"


1 ze 
  d11 pl28:f2 pl16:f3
2 d1 do:f2 do:f3
3 d12
  (p1 ph1)
  d4 pl2:f2
  (center (p2 ph1) (p4 ph6):f2 )
  d4 UNBLKGRAD
  p28 ph1
  d13
  (p1 ph2)

  d13
  p16:gp3          
  d16 pl0:f2 pl16:f3

  (p15:sp10 ph3):f2
  d0 cpd3:f3
  (p2 ph1)
  d0
  p16:gp1*EA
  d16 pl0:f2
  (p10:sp25 ph1):f2
  4u
  DELTA pl0:f2
  (center (p1 ph1) (p15:sp12 ph4):f2 )
  4u
  d24 pl2:f2 
  (center (p2 ph1) (p4 ph1):f2 )
  4u
  d24 pl0:f2
  (center (p1 ph2) (p15:sp10 ph5):f2 )
  4u
  d4 pl2:f2
  (center (p2 ph1) (p4 ph1):f2 )
  4u
  d4
  (p1 ph1)
  DELTA1 
  (p2 ph1)
  p16:gp2
  d16 pl28:f2
  4u BLKGRAD
  go=2 ph31 cpds2:f2 
  d1 do:f2 do:f3 mc #0 to 2
     F1EA(calgrad(EA) & calph(ph5, +180), caldel(d0, +in0) & calph(ph3, +180) & calph(ph6, +180) & calph(ph31, +180))
exit 
  

ph1=0
ph2=1
ph3=0 2
ph4=0 0 2 2  
ph5=1 1 3 3
ph6=0
ph31=0 2 2 0 


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;pl28: f2 channel - power level for selective C5' decoupling
;sp10: f2 channel - shaped pulse  90 degree  (on resonance)
;sp12: f2 channel - shaped pulse  90 degree  (on resonance)
;                  for time reversed pulse
;sp15: f2 channel - shaped pulse 180 degree for decoupling
;                                           (C5' on resonance)
;sp25: f2 channel - shaped pulse 180 degree  (on resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p10: f2 channel - 180 degree shaped pulse
;p15: f2 channel -  90 degree shaped pulse
;p16: homospoil/gradient pulse
;p28: f1 channel - trim pulse 				[1 msec]
;p31: f2 channel - 180 degree pulse for decoupling sequence
;d0 : incremented delay (2D)                            [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(HC))
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d13: short delay                                       [4 usec]
;d16: delay for homospoil/gradient recovery
;d24: 1/(4J)HC for CH
;     1/(8J)HC for all multiplicities
;cnst2: = J(HC)
;cnst11: for multiplicity selection = 4 for CH, 8 for all multiplicities
;o2p: C5' chemical shift
;inf1: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(C)) = DW(C)
;nd0: 2
;ns: 4 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: echo-antiecho
;cpds2: decoupling according to sequence defined by cpdprg2 : mlevsp180.p31
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2 : gp 3
;	                  80 : 20.1 :   30 

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1% 
;gpz3: 30% 

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p15 (sp10/sp12) use Q5/Q5tr pulse (DeltaOmega * DeltaT = 6.180)
;      to cover 20.5ppm on resonance
;      (pulselength: 2.0ms at 600.13 MHz)
;for p10 (sp25) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 11.4ppm on or off resonance
;      (pulselength: 2.0ms at 600.13 MHz)
;for p31 (sp15) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 11.4ppm on or off resonance
;      (pulselength: 2.0ms at 600.13 MHz)



;$Id: na_hsqcetgpjcsp,v 1.6 2012/01/31 17:49:27 ber Exp $

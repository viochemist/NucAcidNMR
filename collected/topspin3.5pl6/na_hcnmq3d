;na_hcnmq3d
;avance-version (15/02/27)
;MQ-HCN
;3D sequence with
;   inverse correlation for triple resonance
;      via MQ and inept transfer steps
;
;      F1(H6/8) -> F2(C6/8) -> F3(N1/9,t1)
;                    -> F2(C6/8,t2) -> F1(H6/8,t3)
;      and/or
;      F1(H1') -> F2(C1') -> F3(N1/9,t1)
;                    -> F2(C1',t2) -> F1(H1',t3)
;
;phase sensitive (t1)
;phase sensitive (t2)
;using constant time in t2
;only for D2O solution
;(use parameterset NA_HCNMQ3D)
;
;J.P. Marino, J.L. Diener, P.B. Moore & C. Griesinger,
;   J. Am. Chem. Soc. 119, 7361-7366 (1997) 
;(R. Fiala, F. Jiang & V. Sklenar, 
;   J. Biomol. NMR 12, 373-383 (1998) )
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"


"d0=3u"
"d10=d22/4-larger(p14,p30)/2-p19-d16"
"d30=d22/4-larger(p14,p30)/2-p19-d16"

"in0=inf1/2"
"in10=inf2/4"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"DELTA=d0*2+larger(p2,p8)"
"DELTA1=d4-larger(p2,p14)/2-p19-d16"
"DELTA2=d4-larger(p2,p14)/2-p3-p19-d16"
"DELTA3=d22/4-larger(p14,p30)/2-p16-d16"
"DELTA4=larger(p14,p30)/2"


"spoff3=0"
"spoff9=0"
"spoff13=0"


aqseq 321


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d1 pl2:f2 pl3:f3
  50u UNBLKGRAD
  
  (p1 ph1)
  p19:gp3
  d16
  DELTA1
  (center (p2 ph1) (p14:sp3 ph1):f2 )
  DELTA2
  p19:gp3
  d16 pl2:f2

  (p3 ph3):f2
  p16:gp4
  d16
  DELTA3
  DELTA4
  (p2 ph1)
  DELTA3
  p16:gp4
  d16
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  p16:gp4
  d16 pl2:f2
  DELTA3
  (p2 ph1)
  DELTA4
  DELTA3
  p16:gp4
  d16
  (p1 ph2) (p3 ph2):f2

  p16:gp5
  d16

  (p21 ph5):f3
  d0 
  (center (p2 ph1) (p8:sp13 ph6):f2 )
  d0
  (p22 ph1):f3
  DELTA
  (p21 ph1):f3

  p16:gp6
  d16 pl2:f2

  (ralign (p1 ph2) (p3 ph2):f2 )
  p19:gp7
  d16
  DELTA4
  d30
  (p2 ph1)
  d30
  p19:gp7
  d16
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  p19:gp7
  d16 pl2:f2
  d10
  (p2 ph1)
  d10
  DELTA4
  p19:gp7
  d16
  (p3 ph4):f2

  p19:gp8
  d16
  DELTA2
  (center (p2 ph1) (p14:sp3 ph1):f2 )
  DELTA1
  p19:gp8
  d16 pl12:f2 pl16:f3

  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph5, +90), caldel(d0, +in0))
     F2PH(calph(ph4, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=0 0 0 0 2 2 2 2
ph31=0 2 2 0 2 0 0 2
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;sp3 : f2 channel - shaped pulse 180 degree (C6/8 or/and C1')
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                          [1 msec]
;p19: gradient pulse 2                                  [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                      [3 usec]
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(HC)) for both:                              [1.4 msec]
;                for H1':                               [1.56 msec]
;                for H8':                               [1.25 msec]
;d10: incremented delay (F2 in 3D) = d22/4-larger(p14,p30)/2-p19-d16
;d11: delay for disk I/O                                [30 msec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(2J(CN)                                         [30 msec]
;d30: decremented delay (F2 in 3D) = d22/4-larger(p14,p30)/2-p19-d16
;cnst2: = J(CH)
;o2p: 90(C1') or 137(C6/C8) or 113.5 ppm(both)
;o3p: 157 ppm
;inf1: 1/SW(N) = 2 * DW(N)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(N)) = DW(N)
;nd0: 2
;in10: 1/(4 * SW(C)) = (1/2) * DW(C)
;nd10: 4
;in30: = in10
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5 : gp 6 : gp 7 : gp 8
;			   5 :   -7 :  -60 :   30 :   13 :    5

;for z-only gradients:
;gpz3: 5%
;gpz4: -7%
;gpz5: -60%
;gpz6: 30%
;gpz7: 13%
;gpz8: 5%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100
;gpnam7: SMSQ10.100
;gpnam8: SMSQ10.100


;Processing

;F2 reverse: true


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   either one pulse to cover 22.8ppm (C1' or C6/C8 in separate 
;      experiments)
;   (pulselength: 1ms at 600.13 MHz)
;   or a twofold modulated pulse to cover 8.8ppm at 90ppm (C1') 
;      and 137ppm (C6/C8) (both in one experiment)
;   (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcnmq3d,v 1.6.2.1 2015/03/03 11:21:24 ber Exp $

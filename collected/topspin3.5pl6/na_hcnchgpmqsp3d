;na_hcnchgpmqsp3d
;avance-version (15/02/27)
;MQ-HCNCH
;3D sequence with
;   inverse correlation for triple resonance
;      via MQ and inept transfer steps
;
;      F1(H1',t1) -> F2(C1') -> F3(N1/9,t2)
;                    -> F2(C6/8) -> F1(H6/8,t3)
;
;using shaped pulses for 180degree pulses on f1
;using twofold modulated shaped pulse on f2
;phase sensitive (t1)
;phase sensitive (t2)
;using constant time in t2
;(use parameterset NA_HCNCHMQSGP3D)
;
;R. Fiala, F. Jiang & V. Sklenar, 
;   J. Biomol. NMR 12, 373-383 (1998)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


define delay DELTA1a
define delay DELTA1b
define delay DELTA2a
define delay DELTA2b
define delay DELTA3a
define delay DELTA3b
define delay DELTA4a
define delay DELTA4b
define delay DELTA5a
define delay DELTA5b
define delay DELTA6a
define delay DELTA6b
define delay DELTA7a
define delay DELTA7b


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"

"d4=1.6m"
"d22=18m"
"d23=17m"
"d25=16m"
"d27=1.25m"


"d0=3u"
"d10=3u"
"d30=d23"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"DELTA1a=d4-d0-larger(p8,p22)/2-p19-d16"
"DELTA1b=d27-larger(p2,p8)/2-p19-d16"
"DELTA2a=d4+d0+larger(p8,p22)/2-p3-p19-d16"
"DELTA2b=d27-larger(p2,p8)/2-p3-p19-d16"
"DELTA3a=d22/2-p12/2-p16-d16"
"DELTA3b=d25/2-p12/2-p16-d16"
"DELTA4a=d22/2-p12/2-p22/2-p16-d16"
"DELTA4b=d25/2-p12/2-p30/2-p16-d16"
"DELTA5a=d4-p14/2-p22/2"
"DELTA5b=d27-p14/2-p30/2"
"DELTA6a=d22/2-d4-p14/2-p12/2-p16-d16"
"DELTA6b=d25/2-d27-p14/2-p12/2-p16-d16"
"DELTA7a=d4*2-p1"
"DELTA7b=d27*2-p1"
"DELTA8=d23-d10-p2"


"spoff3=0"
"spoff9=0"
"spoff13=0"
"spoff23=bf1*(cnst17/1000000)-o1"
"spoff24=bf1*(cnst16/1000000)-o1"
"spoff0=bf2*((cnst22-cnst21)/2000000)"


aqseq 312


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl2:f2 pl3:f3
  50u UNBLKGRAD
;  30u fq=cnst35(bf ppm):f2
  30u fq=cnst21(bf ppm):f2
  
  (p1 ph3)
  p19:gp3
  d16
  DELTA1a
  d0
  (center (p8:sp13 ph1):f2 (p22 ph1):f3 )
  d0
  (p2 ph1)
  DELTA2a
  p19:gp3
  d16 pl2:f2

  (p3 ph4):f2
  p16:gp4
  d16
  DELTA3a
  (p12:sp23 ph1)
  DELTA4a
  p16:gp4
  d16
  (p22 ph1):f3
  DELTA5a
  (p14:sp3 ph1):f2 
  p16:gp4
  d16 pl2:f2
  DELTA6a
  (p12:sp23 ph1)
  DELTA3a
  p16:gp4
  d16 pl1:f1
  (p1 ph5) 
  DELTA7a
  (p3 ph6):f2

  p16:gp5
  d16

  (p21 ph7):f3
  d10 
  (p2 ph1) 
  DELTA8
  (center (p23:sp0 ph1):f2 (p22 ph1):f3 )
  d30
  (p21 ph8):f3

  p16:gp6
;  d16 fq=cnst33(bf ppm):f2
  d16 fq=cnst22(bf ppm):f2
  4u pl2:f2

  (p3 ph9):f2
  DELTA7b
  (p1 ph2) 
  p16:gp7
  d16
  DELTA3b
  (p12:sp24 ph1)
  DELTA6b
  p16:gp7
  d16
  (p14:sp3 ph1):f2
  DELTA5b
  (p30:sp9 ph1):f3
  p16:gp7
  d16 pl2:f2
  DELTA4b
  (p12:sp24 ph1)
  DELTA3b
  p16:gp7
  d16 pl1:f1
  (p3 ph1):f2

  p19:gp8
  d16
  DELTA2b
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  DELTA1b
  p19:gp8
  d16 pl12:f2 pl16:f3

  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
     F2PH(calph(ph7, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit
   

ph1=0 
ph2=1
ph3=0
ph4=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph5=1 1 3 3
ph6=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph7=0 2
ph8=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
    2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph9=0 0 0 0 2 2 2 2
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2
     0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp0 : f2 channel - shaped pulse 180 degree (C6/8 and C1')
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;sp23: f1 channel - shaped pulse 180 degree (H1' off resonance)
;sp24: f1 channel - shaped pulse 180 degree (H6/8 off resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p12: f1 channel - 180 degree shaped pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p23: f2 channel - 180 degree shaped pulse (twofold modulated)
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(C1'H1'))                                   [1.6 msec]
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(4J(C1'N))                                     [18 msec]
;d23: 1/(4J(C1'N)) and 1/(4J(C6/8N))                   [17 msec]
;d25: 1/(4J(C6/8N))                                    [16 msec]
;d27: 1/(4J(C6/8H6/8))                                 [1.25 msec]
;d30: decremented delay (F2 in 3D) = d23
;cnst16: H6/8 chemical shift (offset, in ppm)
;cnst17: H1' chemical shift (offset, in ppm)
;cnst21: C1' chemical shift (offset, in ppm)           [90 ppm]
;cnst22: C6(CU)/C8(AG) chemical shift (offset, in ppm) [137 ppm]
;o3p: 157 ppm
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(N)) = DW(N)
;nd10: 2
;in30: = in10
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5 : gp 6 : gp 7 : gp 8
;			 -10 :   12 :  -70 :   40 :   16 :   14

;for z-only gradients:
;gpz3: -10%
;gpz4: 12%
;gpz5: -70%
;gpz6: 40%
;gpz7: 16%
;gpz8: 14%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100
;gpnam7: SMSQ10.100
;gpnam8: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p12 (sp23,sp24) use Reburp pulse (DeltaOmega * DeltaT = 5.814)
;   to cover 2.95ppm (H1')
;   (pulselength: 3.3ms at 600.13 MHz)
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p23 (sp0) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      twofold modulated to cover 8.8ppm at 90ppm (C1')
;         and 137ppm (C6/C8) with carrier at 113.5ppm
;      (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcnchgpmqsp3d,v 1.3.2.1 2015/03/03 11:21:24 ber Exp $

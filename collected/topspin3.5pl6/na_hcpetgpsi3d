;na_hcpetgpsi3d
;avance-version (15/02/27)
;HCP
;3D sequence with
;   inverse correlation for triple resonance using multiple
;      inept transfer steps
;
;      F1(H) -> F2(C) -> F3(P,t1) -> F2(C,t2) -> F1(H,t3)
;
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho gradient selection (t2)
;using constant time in t2
;with decoupling during acquisition
;(use parameterset NA_HCPETGPSI3D)
;
;J.P. Marino, H. Schwalbe, C. Anklin, W. Bermel, D.M. Crothers 
;   & C. Griesinger, J. Am. Chem. Soc. 116, 6472 - 6473 (1994)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"
"d13=4u"
"d22=1s/(cnst5*8)"
"d24=1s/(cnst2*cnst11)"


"d0=3u"
"d10=3u"
"d30=d22-p16-d16-p22"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"DELTA=d0*2+larger(p2,p4)"
"DELTA1=d22-p16-d16"
"DELTA2=d22-larger(p2,p22)-d16-p16-d10"
"DELTA3=d13+p16+d16+4u"
"DELTA4=d22-p16-d16-p22"


aqseq 321


1 ze 
  d11 pl12:f2 pl16:f3
2 d1 do:f2 do:f3
3 d12
  (p1 ph1)
  d4 pl2:f2 pl3:f3
  (center (p2 ph1) (p4 ph1):f2 )
  d4 UNBLKGRAD             
  (p1 ph2)

  d13
  p16:gp1
  d16
  
  (p3 ph3):f2
  DELTA1
  p16:gp2
  d16
  (p4 ph1):f2
  (p22 ph4):f3
  p16:gp2
  d16
  DELTA4
  (p3 ph2):f2
 
  (p21 ph5):f3
  d0
  (center (p2 ph1) (p4 ph6):f2 )
  d0
  (p22 ph1):f3
  DELTA
  (p21 ph1):f3

  d13
  p16:gp1
  d16
  
  (p3 ph7):f2
  d10
  (center (p2 ph1) (p22 ph1):f3 )
  DELTA2
  p16:gp3*EA*-1
  d16
  (p4 ph1):f2
  (p22 ph1):f3
  p16:gp3*EA
  d16
  d30
 
  (center (p1 ph1) (p3 ph8):f2 )
  d24
  (center (p2 ph1) (p4 ph1):f2 )
  d24
  (center (p1 ph2) (p3 ph9):f2 )
  d4
  (center (p2 ph1) (p4 ph1):f2  )
  d4
  (p1 ph1)  
  DELTA3 pl12:f2 pl16:f3
  (p2 ph1) 
  d13
  p16:gp4
  d16
  4u  BLKGRAD                 
  go=2 ph31 cpd2:f2 cpd3:f3
  d1 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph4, +90) & calph(ph5, +90), caldel(d0, +in0))
     F2EA(calgrad(EA) & calph(ph9, +180), caldel(d10, +in10) & caldel(d30, -in30) & calph(ph6, +180) & calph(ph7, +180) & calph(ph31, +180))
exit 
  

ph1=0
ph2=1
ph3=0 0 0 0 2 2 2 2 
ph4=0   
ph5=0 2
ph6=0
ph7=1
ph8=0 0 2 2 
ph9=3 3 1 1 
ph31=0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse				[1 ms]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (F1 in 3D)			[3 us]
;d1 : relaxation delay; 1-5 * T1		
;d4 : 1/(4J(HC))					[1.6 ms]
;d10: incremented delay (F2 in 3D)			[3 us]
;d11: delay for disk I/O                               	[30 ms]
;d12: delay for power switching                         [20 us]
;d13: short delay                                      	[4 us]
;d16: delay for homospoil/gradient recovery
;d22: 1/(4J(CP)) :  C4'-P                               [12.5 ms]
;                   C5'-P                               [25 ms]
;     and constant time delay T(C) = 1/(J(CC)) with J(CC)=40 Hz
;d24: 1/(4J)HC for CH
;     1/(8J)HC for all multiplicities
;d30: decremented delay (F2 in 3D) = d22-d13-p16-d16
;cnst2: = J(HC)
;cnst5: = J(CP)
;cnst11: for multiplicity selection = 4 for CH, 8 for all multiplicities
;inf1: 1/SW(P) = 2 * DW(P)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(P)) =  DW(P)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in30: = in10
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:  	gp 1 : gp 2 : gp 3 : gp 4
;			  40 :   25 :   40 : 20.1 

;for z-only gradients:
;gpz1: 40%
;gpz2: 25%
;gpz3: 40%
;gpz4: 20.1%  

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100
;gpname4: SMSQ10.100



;$Id: na_hcpetgpsi3d,v 1.5.2.1 2015/03/03 11:21:24 ber Exp $

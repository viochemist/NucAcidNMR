;na_noesyhsqcf3gpwg3d
;avance-version (12/01/11)
;NOESY-HSQC
;3D sequence with
;   homonuclear correlation via dipolar coupling
;   dipolar coupling may be due to noe or chemical exchange.
;   H-1/X correlation via double inept transfer
;phase sensitive (t1)
;phase sensitive (t2)
;using flip-back pulse
;water suppression using watergate sequence
;using f3 - channel
;proton region shifted by ip
;using different offsets on f3 during sequence
;(use parameterset NA_NOESYHSQCF3GPWG3D)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d26=1s/(cnst4*4)"


"d0=3u"
"d10=3u"

"in0=inf1/2"
"in10=inf2/2"


"DELTA2=d26-p16-d16"
"DELTA3=d26-p16-d16-p11-8u"

#   ifdef LABEL_CN
"DELTA=d10*2+larger(p2,p8)"
"DELTA1=d0*2+larger(p8,p22)"
#   else
"DELTA=d10*2+p2"
"DELTA1=d0*2+p22"
#   endif /*LABEL_CN*/

"TAU=d8-p16-d16-p29-4u"


"spoff13=0"


"cnst0=(cnst30+cnst31)/2"


aqseq 321


1 ze
  d11 pl16:f3
2 d11 do:f3
3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1
  50u UNBLKGRAD

  30u fq=cnst0(bf ppm):f3
  (p29:sp11 ph8:r):f1
  2u
  2u pl1:f1
  (p1 ph8)
  DELTA1 pl3:f3
  (p2 ph9)
  d0

#   ifdef LABEL_CN
  (center (p8:sp13 ph1):f2 (p22 ph1):f3 )
#   else
  (p22 ph1):f3
#   endif /*LABEL_CN*/

  d0
  (p1 ph10)

  4u
  (p29:sp11 ph12:r):f1
  p16:gp3
  d16 pl1:f1
  TAU fq=cnst30(bf ppm):f3

  (p1 ph11)
  p16:gp4
  d16
  DELTA2
  (center (p2 ph2) (p22 ph6):f3 )
  DELTA2
  p16:gp4
  d16
  (p1 ph2) 

  4u
  (p29:sp11 ph11:r):f1
  p16:gp5
  d16 pl1:f1

  (p21 ph3):f3
  d10 

#   ifdef LABEL_CN
  (center (p2 ph7) (p8:sp13 ph1):f2 )
#   else
  (p2 ph7)
#   endif /*LABEL_CN*/

  d10
  (p22 ph4):f3
  DELTA
  (p21 ph4):f3

  p16:gp5
  d16
  (p29:sp11 ph13:r):f1
  2u
  2u pl1:f1

  (p1 ph1)
  4u
  p16:gp6
  d16
  DELTA3
  (p11:sp1 ph13:r):f1
  2u
  2u pl1:f1
  (center (p2 ph1) (p22 ph1):f3 )
  4u
  (p11:sp1 ph13:r):f1
  p16:gp6
  d16
  DELTA3 pl16:f3
  4u BLKGRAD

  go=2 ph31 cpd3:f3
  d11 do:f3 mc #0 to 2 
     F1PH(calph(ph8, +90) & calph(ph9, +90) & calph(ph29, +90), caldel(d0, +in0) & calph(ph8, +90) & calph(ph9, +90) & calph(ph29, +90)) 
     F2PH(calph(ph3, +90), caldel(d10, +in10))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 2 2
ph6=0
ph7=0 0 2 2
ph8=0 0 0 0 2 2 2 2
ph9=1 1 1 1 3 3 3 3
ph10=0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
     2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
ph11=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph12=2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
     0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
ph13=2
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
     2 0 0 2 0 2 2 0 0 2 2 0 2 0 0 2





;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp1 : f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp11: f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8: f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p11: f1 channel -  90 degree shaped pulse             [1 msec]
;p16: homospoil/gradient pulse                         [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p29: f1 channel -  90 degree shaped pulse             [1 msec]
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d26: 1/(4J(NH))
;cnst4: = J(NH)
;cnst0:  ( cnst30 + cnst31 ) / 2
;cnst30: N(H) chemical shift (offset, in ppm)          [151 ppm]
;           set to N(imino)
;cnst31: N(H2) chemical shift (offset, in ppm)         [81 ppm]
;           set to N(amino)
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(N)) = DW(N)
;nd10: 2
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 3 : gp 4 : gp 5 : gp 6
;                         20 :   50 :   80 :   30

;for z-only gradients:
;gpz3: 20%
;gpz4: 50%
;gpz5: 80%
;gpz6: 30%

;use gradient files:
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100


;Processing

;SR(F1): -1/4 SWH(F1)



                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping



;$Id: na_noesyhsqcf3gpwg3d,v 1.8 2012/01/31 17:49:27 ber Exp $

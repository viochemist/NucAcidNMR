;na_trhnncosygpphspwg
;avance-version (12/01/11)
;TROSY-HNN-COSY
;2D H-1/N-15 correlation via multiple inept transfer
;for direct observation of hydrogen bonds in nucleic acid
;   base pairs by internucleotide 2J(N,N) couplings
;
;   for UA:
;      F1(H3) -> F3(N3) -> F3(N1,t1) -> F3(N3) -> F1(H3,t2)
;
;   for GC:
;      F1(H1) -> F3(N1) -> F3(N3,t1) -> F3(N1) -> F1(H1,t2)
;
;phase sensitive
;using shaped 180degree pulses on f3
;(use parameterset NA_TRHNNCOSYGPPHSPWG)
;
;A.J. Dingley & S. Grzesiek, 
;   J. Am. Chem. Soc. 120, 8293-8297 (1998)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d12=20u"

"d26=2.25m"


"in0=inf1"

"d0=in0/2-p21*4/3.1416"


"DELTA1=d26-p16-d16-p32/2"
"DELTA2=d27/2-p16-d16-p32/2"
"DELTA3=d27/2-p19-d16-p32/2"
"DELTA4=d26-p29-p16-d16-p1-4u"
"DELTA5=d26-p29-p16-d16-4u"
"DELTA6=d26+p19-p16-p11-4u"
"DELTA7=p11-p19/2-d16/2-p21/2-p32/2+p2/2+2u"
"DELTA8=d26-p16-d16-p11-p21-8u"

"TAU=p21*4/3.1416"


"spoff14=0"


1 ze 
2 d1
3 d12 pl1:f1
  50u UNBLKGRAD

  (p1 ph1)
  p16:gp3
  d16
  DELTA1
  (center (p2 ph2) (p32:sp14 ph1):f3 )
  DELTA1
  p16:gp3
  d16
  (p1 ph2) 

  4u
  (p29:sp11 ph9:r):f1
  4u
  p16:gp4
  d16

  (p21 ph3):f3
  p16:gp5
  d16
  DELTA2
  (p32:sp14 ph1):f3
  DELTA2
  p16:gp5
  d16
  TAU
  p16:gp5
  d16
  DELTA2
  (p32:sp14 ph1):f3
  DELTA2
  p16:gp5
  d16
  (p21 ph4):f3

  d0

  (p21 ph5):f3
  p19:gp6
  d16
  DELTA3
  (p32:sp14 ph1):f3
  DELTA3
  p19:gp6
  d16
  p19:gp6
  d16
  DELTA3
  (p32:sp14 ph1):f3
  DELTA3
  p19:gp7
  d16

  (p1 ph2) 
  4u
  (p29:sp11 ph10:r):f1
  p16:gp8
  d16
  DELTA4
  (center (p2 ph2) (p22 ph1):f3 )
  DELTA5
  p16:gp8
  d16
  (p29:sp11 ph11:r):f1
  4u

  (center (p1 ph6) (p21 ph7):f3 )

  p16:gp9
  d16
  DELTA6
  (p11:sp1 ph12:r 4u p2 ph1 4u p11:sp1 ph12:r):f1 (DELTA7 p32:sp14 ph1):f3
  DELTA8

  p16:gp9
  d16

  (p21 ph8):f3
  p19:gp2
  d16
  4u BLKGRAD
  go=2 ph31
  d1 mc #0 to 2 F1PH(calph(ph3, +90) & calph(ph4, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=1
ph3=0 1 2 3
ph4=1 2 3 0 3 0 1 2
ph5=3 0 1 2 3 0 1 2 1 2 3 0 1 2 3 0
ph6=2
ph7=1
ph8=2
ph9=2
ph10=1
ph11=0
ph12=2
ph29=0
ph31=0 3 2 1


;pl1 : f1 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;sp1 : f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp11: f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp14: f3 channel - shaped pulse 180 degree (adiabatic)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p11: f1 channel -  90 degree shaped pulse              [1 msec]
;p16: homospoil/gradient pulse
;p19: gradient pulse 2                                  [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p29: f1 channel -  90 degree shaped pulse              [1 msec]
;p32: f3 channel - 180 degree shaped pulse for inversion (adiabatic)
;d0 : incremented delay (2D) = in0/2-p21*4/3.1416
;d1 : relaxation delay; 1-5 * T1
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d26 : 1/(4J(NH))                                       [2.25 msec]
;d27 : 1/(4J(NN))                                       [15 msec]
;o2p: 152 ppm
;o3p: 160 ppm
;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/(1 * SW(N)) = 2 * DW(N)
;nd0: 1
;ns: 16 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: States-TPPI (or TPPI)
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:  gp2 : gp3 : gp4 : gp5 : gp6 : gp7 : gp8 : gp9
;                     8.1 : -19 :  45 :  -7 :   2 :  82 :   5 :  23

;for z-only gradients:
;gpz2: 8.1%
;gpz3: -19%
;gpz4: 45%
;gpz5: -7%
;gpz6: 2%
;gpz7: 82%
;gpz8: 5%
;gpz9: 23%

;use gradient files:
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100
;gpnam7: SMSQ10.100
;gpnam8: SMSQ10.100
;gpnam9: SMSQ10.100


;Processing

;PHC0(F2): 90
;PHC1(F2): -180
;FCOR(F2): 1



;$Id: na_trhnncosygpphspwg,v 1.5 2012/01/31 17:49:27 ber Exp $

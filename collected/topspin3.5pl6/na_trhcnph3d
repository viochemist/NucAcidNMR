;na_trhcnph3d
;avance-version (15/02/27)
;TROSY-HCN
;3D sequence with
;   inverse correlation for triple resonance
;      via TROSY and inept transfer steps
;
;      F1(H6/8) -> F2(C6/8,t1) -> F3(N1/9,HMQC,t2) 
;                    -> F2(C6/8,t1) -> F1(H6/8,t3)
;      and
;      F1(H1') -> F2(C1',t1) -> F3(N1/9,HMQC,t2) 
;                    -> F2(C1',t1) -> F1(H1',t3)
;
;using twofold modulated shaped pulse
;phase sensitive (t1)
;phase sensitive (t2)
;using constant time in t1
;(use parameterset NA_TRHCNPH3D)
;
;R. Riek, K. Pervushin, C. Fernandez, M. Kainosho & 
;   K. Wuethrich, J. Am. Jacs. Soc. 123, 658-664 (2001)
;(R. Fiala, J. Czernek & V. Sklenar, 
;   J. Biomol. NMR 16, 291-302 (2000) )
;(B. Brutscher & J.-P. Simorre,
;   J. Biomol. NMR 21, 367-372 (2001) )
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"


"in0=inf1/2"
"in10=inf2"

"in20=in0"

"d0=d22-p23/2-p19-d16"
"d10=in10/2-p21*4/3.1416"
"d20=d22-p23/2-p19-d16"

"td1=tdmax(td1,d20*2,in20)"


"DELTA1=d4-larger(p2,p8)/2-p19-d16"


"spoff0=0"
"spoff13=0"


aqseq 312


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl2:f2 pl3:f3
  50u UNBLKGRAD
  
  (p1 ph1)
  p19:gp3
  d16
  DELTA1
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  DELTA1
  p19:gp3
  d16 pl2:f2
  (p1 ph2) 

  (p3 ph3):f2
  p19:gp4
  d16
  d0
  (center (p23:sp0 ph1):f2 (p21 ph4 d10 p21 ph1):f3 )
  d20
  p19:gp4
  d16 pl2:f2
  (p3 ph1):f2

  (p1 ph1) 
  4u
  p19:gp5
  d16
  DELTA1
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  DELTA1
  p19:gp5
  d16 pl12:f2 pl16:f3
  4u BLKGRAD
  (p28 ph1)

  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0) & caldel(d20, -in20))
     F2PH(calph(ph4, +90), caldel(d10, +in10))
exit
   

ph1=0 
ph2=1
ph3=0
ph4=0 2
ph29=0
ph31=0 2
     

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp0 : f2 channel - shaped pulse 180 degree (C6/8 and C1')
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p19: gradient pulse 2                                  [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p23: f2 channel - 180 degree shaped pulse (twofold modulated)
;p28: f1 channel - trim pulse                           [1 msec]
;d0 : incremented delay (F1 in 3D) = d22-p23/2-p19-d16
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(CH))                                        [1.4 msec]
;d10: incremented delay (F2 in 3D) = in10/2-p21*4/3.1416
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d20: decremented delay (F1 in 3D) = d22-p23/2-p19-d16
;d22: 1/(2J(CN)                                         [30 msec]
;cnst2: = J(CH)
;o2p: 113.5 ppm
;o3p: 157 ppm
;inf1: 1/SW(C) = 2 * DW(C)
;inf2: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(C)) = DW(C)
;nd0: 2
;in10: 1/(1 * SW(N)) = 2 * DW(N)
;nd10: 1
;in20: = in0
;ns: 2 * n
;ds: >= 32
;td1: number of experiments in F1       td1 max = 2 * d20 / in20
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5
;			  50 :   80 :   30

;for z-only gradients:
;gpz3: 50%
;gpz4: 80%
;gpz5: 30%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;Processing

;PHC0(F2): 90
;PHC1(F2): -180
;FCOR(F2): 1


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p23 (sp0) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   twofold modulated to cover 8.8ppm at 90ppm (C1')
;      and 137ppm (C6/C8) with carrier at 113.5ppm
;   (pulselength: 2.6ms at 600.13 MHz)



;$Id: na_trhcnph3d,v 1.8.2.1 2015/03/03 11:21:24 ber Exp $

;na_trhcchco3d
;avance-version (15/02/27)
;TROSY-HCCH-COSY
;3D sequence with
;   inverse correlation for triple resonance
;      via inept transfer steps
;
;   in A:
;      F1(H2) -> F2(C2 -> C5+C4/6,t1 -> C2,t2) -> F1(H2,t3)
;      and
;      F1(H8) -> F2(C8 -> C4/6+C5,t1 -> C8,t2) -> F1(H8,t3)
;
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho (t2)
;using constant time in t2
;using sensitivity improvement
;(use parameterset NA_TRHCCHCO3D)
;
;B. Simon, K. Zanier & M. Sattler, 
;   J. Biomol. NMR 20, 173-176 (2001)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d11=30m"

"d4=1.25m"
"d22=14.2m"
"d25=7.12m"
"d27=1m"


"d0=3u"
"d10=3u"
"d30=d22-d4-p19-d16+p3*2/3.1416"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"DELTA=d0*2+p22"
"DELTA1=d4-larger(p2,p8)/2"
"DELTA2=d25-p16-d16"
"DELTA3=d22-d25-p15-4u"
"DELTA4=d22-p16-d16-p15-4u"
"DELTA5=d27-p19-d16"
"DELTA6=d22-d4-p22-d10-p19-d16+p1-p3*2/3.1416"
"DELTA7=d4-p19-d16-larger(p2,p8)/2-4u"


"spoff10=bf2*(cnst28/1000000)-o2"
"spoff13=0"


aqseq 321


1 ze
  d11 pl12:f2
2 d11 do:f2 

3 d1 pl2:f2 pl3:f3
  
  (p1 ph1)
  DELTA1
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  DELTA1 UNBLKGRAD
  (p1 ph2) 

  p16:gp3
  d16 pl2:f2

  (p3 ph3):f2

  p16:gp4
  d16
  DELTA2
  (p15:sp10 ph9):f2
  4u
  DELTA3 pl2:f2
  (p4 ph3):f2
  DELTA4
  p16:gp4
  d16
  (p15:sp10 ph2):f2
  2u
  2u pl2:f2
  
  (p3 ph3):f2

  p19:gp5
  d16
  DELTA5
  (p4 ph3):f2
  DELTA5
  p19:gp5
  d16
  
  (p3 ph4):f2
  d0
  (p22 ph1):f3
  d0
  (p4 ph10):f2
  DELTA
  (p3 ph2):f2

  p19:gp6
  d16
  DELTA5
  (p4 ph1):f2
  DELTA5
  p19:gp6
  d16

  (p3 ph1):f2
  d10
  (p22 ph1):f3
  DELTA6
  p19:gp1*EA
  d16
  (p4 ph1):f2
  p19:gp1*-1*EA
  d16
  d30
  (p1 ph5)
  d4
  (center (p2 ph2) (p4 ph7):f2 )
  d4
  (center (p1 ph6) (p3 ph8):f2 )
  DELTA1
  (center (p2 ph1) (p8:sp13 ph7):f2 )
  DELTA7
  p19:gp2
  d16 pl12:f2
  4u BLKGRAD

  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph3, +90) & calph(ph4, +90), caldel(d0, +in0))
     F2EA(calgrad(EA) & calph(ph5, +180), caldel(d10, +in10) & caldel(d30, -in30) & calph(ph7, +180) & calph(ph8, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=1 3
ph5=3
ph6=2
ph7=0
ph8=1 1 3 3
ph9=0 0 0 0 2 2 2 2
ph10=0 0 2 2
ph31=0 2 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp10 : f2 channel - shaped pulse  90 degree (C5 off resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p15: f2 channel -  90 degree shaped pulse
;p16: homospoil/gradient pulse
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(CH))                                       [1.25 msec]
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(4J(C2C5)) and 1/(4J(C8C4/6))                  [14.2 msec]
;d25: 1/(2J(C5C6))                                     [7.1 msec]
;d27: 1/(4J(C5C4/6))                                   [1 msec]
;d30: decremented delay (F2 in 3D) = d22-d4-p19-d16+p3*2/3.1416
;cnst28: C5 (C/U) chemical shift (offset, in ppm)      [105 ppm]
;o2p: 140 ppm,
;o3p: 150 ppm,
;inf1: 1/SW(C) = 2 * DW(C)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(C)) = DW(C)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in30: = in10
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6
;			  80 : 40.2 :   70 :   13 :   15 :   17

;for z-only gradients:
;gpz1: 80%
;gpz2: 40.2%
;gpz3: 70%
;gpz4: 13%
;gpz5: 15%
;gpz6: 17%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p15 (sp10) use Q5 pulse (DeltaOmega * DeltaT = 6.180)
;      to cover 20.5ppm on resonance
;      (pulselength: 2.0ms at 600.13 MHz)



;$Id: na_trhcchco3d,v 1.6.2.1 2015/03/03 11:21:24 ber Exp $

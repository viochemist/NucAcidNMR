;na_h5c5c4n3h_3d
;avance-version (15/02/27)
;H5(C5C4)NH
;3D sequence with
;   inverse correlation for triple resonance
;      via inept transfer steps
;
;   in U:
;      F1(H5,t1) -> F2(C5 -> C4)) -> F3(N3,t2) -> F1(H3,t3)
;
;   in C:
;      F1(H5,t1) -> F2(C5 -> C4)) -> F3(N4,t2) -> F1(H4,t3)
;
;on/off resonance C pulses using shaped pulse
;phase sensitive (t1)
;phase sensitive (t2)
;using constant time in t2
;water suppression using watergate sequence
;(use parameterset NA_H5C5C4N3H_3D)
;
;J. Woehnert, R. Ramachandran, M. Goerlach & L.R. Brown,
;   J. Magn. Reson. 139, 430-433 (1999)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d11=30m"

"d4=1.25m"
"d23=4.1m"
"d25=10m"
"d26=2.5m"


"d0=3u"
"d10=3u"
"d30=d25"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"DELTA1=d4-d0-p14/2"
"DELTA2=d4+d0+p14/2"
"DELTA3=d23-d4-p2"
"DELTA4=d25-d23-p14"
"DELTA5=d25-p14-4u"
"DELTA6=d25-d10-d24-p2"
"DELTA7=d26-p16-d16-p11-8u"


"spoff2=0"
"spoff3=0"
"spoff5=bf2*((cnst24-cnst28)/1000000)"
"spoff7=bf2*((cnst28-cnst24)/1000000)"
"spoff8=0"
"spoff9=0"


aqseq 312


1 ze
  d11 pl16:f3
2 d11 do:f3

3 d1 pl1:f1 pl2:f2 pl3:f3 
  30u fq=cnst28(bf ppm):f2
  50u UNBLKGRAD

  (p3 ph1):f2
  (p21 ph1):f3
  p16:gp3
  d16

  (p1 ph3)
  DELTA1
  d0
  (p14:sp3 ph1):f2
  d0
  (p2 ph1)
  DELTA2
  (p1 ph2) 

  p16:gp4
  d16

  (p13:sp2 ph4):f2
  d4
  (p2 ph1)
  DELTA3
  (p14:sp5 ph1):f2
  4u
  (p14:sp3 ph1):f2
  d23
  (p14:sp5 ph1):f2
  4u
  (p13:sp8 ph1):f2

  4u
  30u fq=cnst24(bf ppm):f2

  (p13:sp2 ph5):f2
  d23
  (p14:sp7 ph1):f2
  DELTA4
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA5
  (p14:sp7 ph1):f2
  4u
  (p13:sp8 ph1):f2

  (p21 ph6):f3
  d10
  d24
  (p2 ph1)
  DELTA6
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  d30
  (p21 ph1):f3

  p16:gp4
  d16

  (p1 ph1) 
  p16:gp5
  d16
  DELTA7 pl0:f1
  (p11:sp1 ph7):f1
  4u
  4u pl1:f1
  (center (p2 ph1) (p22 ph1):f3 )
  4u pl0:f1
  (p11:sp1 ph7):f1
  DELTA7
  p16:gp5
  d16 pl16:f3
  4u BLKGRAD

  go=2 ph31 cpd3:f3 
  d11 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
     F2PH(calph(ph6, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit
   

ph1=0 
ph2=1
ph3=0 0 0 0 2 2 2 2
ph4=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph5=0 0 2 2
ph6=0 2
ph7=2
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
  

;pl0 : 120db
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp1: f1 channel - shaped pulse  90 degree  (H2O on resonance)
;sp2 : f2 channel - shaped pulse  90 degree  (on resonance)
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp5 : f2 channel - shaped pulse 180 degree  (C4 off resonance)
;sp7 : f2 channel - shaped pulse 180 degree  (C5 off resonance)
;sp8 : f2 channel - shaped pulse  90 degree  (on resonance)
;                  for time reversed pulse
;sp9 : f3 channel - shaped pulse 180 degree (N on resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p11: f1 channel -  90 degree shaped pulse             [1 msec]
;p13: f2 channel -  90 degree shaped pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(C5H5))                                     [1.25 msec]
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d16: delay for homospoil/gradient recovery
;d23: 1/(4J(C5C4))                 )                   [4.1 msec]
;d24: for U: 1/((4J)NH))                               [2.7 msec]
;     for C: 1/((8J)NH))                               [1.4 msec]
;d25: 1/(4J(C4N))                                      [10 msec]
;d26: 1/(4J(NH))                                       [2.5 msec]
;d30: decremented delay (F2 in 3D) = d25
;cnst24: C4 (C/U) chemical shift (offset, in ppm)      [169 ppm]
;cnst28: C5 (C/U) chemical shift (offset, in ppm)      [105 ppm]
;o3p: for U: 160 ppm, for C: 100 ppm
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(N) = 2 * DW(N)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(N)) = DW(N)
;nd10: 2
;in30: = in10
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5
;			  50 :   80 :   30

;for z-only gradients:
;gpz3: 50%
;gpz4: 80%
;gpz5: 30%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p13 (sp2/sp8) use Q5/Q5tr pulse (DeltaOmega * DeltaT = 6.180)
;      to cover 41.0ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p14 (sp3, sp5, sp7) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on or off resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_h5c5c4n3h_3d,v 1.6.2.1 2015/03/03 11:21:24 ber Exp $

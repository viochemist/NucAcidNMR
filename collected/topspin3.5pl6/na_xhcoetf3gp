;na_xhcoetf3gp
;avance-version (12/01/11)
;2D P-31/H-1 correlation via inept transfer
;phase sensitive using Echo/Antiecho-TPPI 
;with optional f2 decoupling during acquisition
;(use parameterset NA_XHCOETF3GP)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d3=1s/(cnst2*cnst11)"
"d11=30m"
"d12=20u"


"d0=3u"

"in0=inf1/2"


"DELTA2=p16+d16+4u"

#   ifdef LABEL_CN
"DELTA1=d3-p16-larger(p2,p8)-d0*2"
#   else
"DELTA1=d3-p16-p2-d0*2"
#   endif /*LABEL_CN*/


"spoff13=0"


1 ze

#   ifdef LABEL_CN
  d11 pl12:f2
2 d11 do:f2
  d12 pl2:f2
#   else
2 d11
#   endif /*LABEL_CN*/

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl3:f3 
  50u UNBLKGRAD

  (p21 ph3):f3
  d0 

#   ifdef LABEL_CN
  (center (p2 ph5) (p8:sp13 ph1):f2 )
#   else
  (p2 ph5)
#   endif /*LABEL_CN*/

  d0
  p16:gp1*EA
  DELTA1
  (center (p2 ph1) (p22 ph1):f3 )
  d3
  (p1 ph1) (p21 ph4):f3
  DELTA2
  (p2 ph1)
  p16:gp2

#   ifdef LABEL_CN
  d16 pl12:f2
  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
#   else
  d16
  4u BLKGRAD
  go=2 ph31
  d11 mc #0 to 2 
#   endif /*LABEL_CN*/

     F1EA(calgrad(EA), caldel(d0, +in0) & calph(ph3, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=1 1 1 1 3 3 3 3
ph5=0 0 2 2
ph29=0
ph31=0 2 0 2 2 0 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse                       [1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d3 : 1/(6J(PH))  for all multiplicities
;     1/(4J(PH))  PH only
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d16: delay for homospoil/gradient recovery
;cnst2: = J(PH)
;cnst11: 6  for all multiplicities
;        4  PH only
;o2p: 77 ppm
;o3p: 0 ppm
;inf1: 1/SW(P) = 2 * DW(P)
;in0: 1/(2 * SW(P)) = DW(P)
;nd0: 2
;ns: 1 * n
;ds: >= 32
;td1: number of experiments
;FnMODE: echo-antiecho
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2
;			  80 : 32.4

;for z-only gradients:
;gpz1: 80%
;gpz2: 32.4%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: na_xhcoetf3gp,v 1.7 2012/01/31 17:49:27 ber Exp $

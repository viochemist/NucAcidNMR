;na_hcnchgpjrphsp
;avance-version (13/01/23)
;HCNCH
;2D inverse correlation for triple resonance using multiple
;   inept transfer steps
;
;   F1(H1',t1) -> F2(C1') -> F3(N1/9,t2)
;                               -> F2(C6/8) -> F1(H6/8,t3)
;
;phase sensitive
;water suppression using 1-1 echo pulse sequence
;with decoupling during acquisition
;(use parameterset NA_HCNCHGPJRPHSP)
;
;V. Sklenar, R.D. Peterson, M.R. Rejante, E. Wang & J. Feigon,
;   J. Am. Chem. Soc. 115, 12181 - 12182 (1993) 
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d11=30m"
"d12=20u"
"d13=4u"

"d4=1.56m"        
"d23=17m"
"d25=16m"
"d27=1.25m"


"d0=3u"

"in0=inf1/2"


"DELTA=d4-d0*2-p22-p4"
"DELTA1=d22-d13-p16-d16-larger(p14,p22)/2-d4-p2"
"DELTA2=d22-d13-p16-d16-larger(p14,p22)/2-d4"
"DELTA3=d23-d13-d16-p16-larger(p23,p22)/2"
"DELTA4=d25-d13-d16-p16-larger(p14,p30)/2-d27"
"DELTA5=d25-d13-d16-p16-larger(p14,p30)/2-d27-p2"
"DELTA6=d27-d16-p16"
"DELTA7=d19-p4/2"


"spoff0=0"
"spoff3=bf2*(cnst22/1000000)-o2"
"spoff5=bf2*(cnst21/1000000)-o2"
"spoff9=0"


1 ze 
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3
3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl3:f3

  (p1 ph4)
  d0
  (p22 ph1):f3
  DELTA pl2:f2
  (p4 ph1):f2
  d0
  (p2 ph1) 
  d4 UNBLKGRAD             
  p28 ph1
  d13
  (p1 ph2)

  d13
  p16:gp1
  d16
  
  (p3 ph3):f2
  d4
  (p2 ph1)
  DELTA1
  d13
  p16:gp2
  d16
  (center (p14:sp5 ph1):f2 (p22 ph1):f3 )
  d13
  p16:gp2
  d16 pl2:f2 
  DELTA2
  d4 
  (p3 ph6):f2

  (p21 ph5):f3
  DELTA3
  d13 
  p16:gp3
  d16
  (center (p23:sp0 ph1):f2 (p22 ph1):f3 )
  d13 
  p16:gp3
  d16 pl2:f2
  DELTA3
  (p21 ph1):f3

  d13
  p16:gp4
  d16
  
  (p3 ph1):f2
  d27
  DELTA4
  d13
  p16:gp5
  d16
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  d13
  p16:gp5
  d16 pl2:f2
  DELTA5
  (p2 ph1)
  d27
  (p3 ph1):f2
 
  d13
  p16:gp6
  d16
 
  (p1 ph1)
  d19
  (p1 ph7)
  4u
  DELTA6
  p16:gp7
  d16
  (p1 ph1)
  DELTA7
  (p4 ph1):f2
  DELTA7
  (p1 ph7)
  DELTA6
  p16:gp7
  d16 pl12:f2 pl16:f3
  4u  BLKGRAD                 
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph4, +90) & calph(ph29, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=1
ph3=0 2 
ph4=0
ph5=0 0 0 0 2 2 2 2
ph6=0 0 2 2               
ph7=2
ph29=0
ph31=0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp0 : f2 channel - shaped pulse 180 degree (C6/8 and C1')
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp5 : f2 channel - shaped pulse 180 degree (C1' off resonance)
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse				[1 msec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p23: f2 channel - 180 degree shaped pulse (twofold modulated)
;p28: f1 channel - trim pulse                           [1 msec]
;p30: f3 channel - 180 degree shaped pulse for inversion
;d0 : incremented delay (2D)				[3 usec]
;d1 : relaxation delay; 1-5 * T1		
;d4 : 1/(4J(H1'C1'))					[1.56 msec]
;d11: delay for disk I/O                               	[30 msec]
;d12: delay for power switching                         [20 usec]
;d13: short delay                                      	[4 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d22: 1/(4J(CN))
;        C1': 	                                        [18 msec]
;d23: 1/(4J(CN))
;        C1' and C6/C8:					[17 msec]
;d25: 1/(4J(CN))
;        C6/C8:						[16 msec]
;d27: 1/(4J(H6/8C6/8))                                  [1.25 msec]
;cnst21: C1' chemical shift (offset, in ppm)            [90 ppm]
;cnst22: C6/8 chemical shift (offset, in ppm)           [137 ppm]
;o2p: 113.5 ppm
;inf1: 1/SW(H) = 2 * DW(H)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI)
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:
;                      gp 1 : gp 2 : gp 3 : gp 4 : gp 5 : gp 6 : gp 7
;                       -40 :   25 :  -45 :  -60 :   40 :   55 :   10

;for z-only gradients:
;gpz1: -40%
;gpz2: 25%
;gpz3: -45%
;gpz4: -60%   
;gpz5: 40% 
;gpz6: 55% 
;gpz7: 10% 

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100
;gpname4: SMSQ10.100
;gpname5: SMSQ10.100
;gpname6: SMSQ10.100
;gpname7: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3, sp5) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;      sp3:   C6/C8 off res.
;      sp5:   C1' off res.
;for p23 (sp0) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      twofold modulated to cover 8.8ppm at 90ppm (C1')
;         and 137ppm (C6/C8) with carrier at 113.5ppm
;      (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcnchgpjrphsp,v 1.8 2013/02/05 10:11:25 ber Exp $

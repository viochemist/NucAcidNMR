;na_hcncchcomq3d
;avance-version (15/02/27)
;MQ-HCNCCH-COSY
;3D sequence with
;   inverse correlation for triple resonance
;      via MQ and inept transfer steps
;
;      F1(H1') -> F2(C1') -> F3(N1/9,t1)
;              -> F2(C1' -> C2',t2) -> F1(H',t3)
;
;phase sensitive (t1)
;phase sensitive using Echo/Antiecho (t2)
;using semi-constant time in t2
;using sensitivity improvement
;(use parameterset NA_HCNCCHCOMQ3D)
;
;W. Hu, L. Jiang & Y.Q. Gosser, 
;   J. Magn. Reson. 145, 147-151 (2000)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


define delay DELTA2a
define delay DELTA2b
define delay DELTA3a
define delay DELTA3b


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d11=30m"
"d12=20u"

"d4=1.56m"
"d23=18m"
"d25=15m"
"d27=3.2m"


"d0=3u"
"d10=3u"
"d29=d27-d4-p19-d16-d10-p2-p22"
"d30=d27"

"in0=inf1/2"
"in10=inf2/2"

"FACTOR1=d30*10000000*2/td2"
"INCR2=FACTOR1/10000000"

"if ( INCR2 > in10 ) { in29 = 0; } else { in29=in10-INCR2; }"
"if ( INCR2 > in10 ) { in30 = in10; } else { in30 = INCR2; }"


"DELTA=d0*2+larger(p2,p8)"
"DELTA1=p3*2"
"DELTA2a=d23/2-d4-larger(p14,p30)/2"
"DELTA2b=d25/2-d4-larger(p4,p30)/2"
"DELTA3a=d23/2-larger(p14,p30)/2"
"DELTA3b=d25/2-larger(p4,p30)/2"
"DELTA4=d25/2-p1*2/3.1416"
"DELTA5=d25/2-d4-p1*2/3.1416"
"DELTA6=d4*2-p1"
"DELTA7=d4-p19-d16"
"DELTA8=p19+d16+4u"


"spoff3=0"
"spoff9=0"
"spoff13=0"


"cnst30=(cnst21+cnst23)/2"


aqseq 321


1 ze
  d11 pl12:f2
2 d11 do:f2 

3 d12 pl32:f1 pl2:f2
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1
  30u fq=cnst21(bf ppm):f2
  50u UNBLKGRAD

  (p1 ph1)
  d4*2
  DELTA1
  (p2 ph1)
  (p3 ph1):f2
  d4
  d23*0.5
  (p2 ph1)
  DELTA2a
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA3a
  (p2 ph1)
  d23*0.5 pl2:f2
  (p3 ph2):f2
  (p1 ph3)

  p16:gp3
  d16 pl3:f3

  (p21 ph4):f3
  d0 
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  d0
  (p22 ph1):f3
  DELTA pl2:f2
  (p21 ph1):f3

  p16:gp4
  d16 fq=cnst30(bf ppm):f2

  (ralign (p1 ph1) (p3 ph5):f2 )
  DELTA4
  (p2 ph1)
  DELTA3b
  (center (p4 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA2b
  (p2 ph1)
  DELTA5 pl3:f3
  (p1 ph1)
  DELTA6
  (p3 ph1):f2

  d30
  (p4 ph1):f2
  p19:gp1*EA
  d16
  d29
  (p2 ph1)
  d4
  (p22 ph1):f3
  d10

  (center (p1 ph1) (p3 ph6):f2 )

  p19:gp5
  d16
  DELTA7
  (center (p2 ph1) (p4 ph1):f2 )
  DELTA7
  p19:gp5
  d16
  (center (p1 ph2) (p3 ph7):f2 )
  p19:gp5
  d16
  DELTA7
  (center (p2 ph1) (p4 ph1):f2 )
  DELTA7
  p19:gp5
  d16
  (p1 ph1)
  DELTA8
  (p2 ph1)
  p19:gp2
  d16 pl12:f2

  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph4, +90), caldel(d0, +in0))
     F2EA(calgrad(EA) & calph(ph7, +180), caldel(d10, +in10) & caldel(d29, +in29) & caldel(d30, -in30) & calph(ph5, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=1 1 1 1 3 3 3 3
ph4=0 0 2 2
ph5=1 3
ph6=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph7=3 3 3 3 3 3 3 3 1 1 1 1 1 1 1 1
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(C1'H1'))                                   [1.56 msec]
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d23: 1/(4J(C1'N))                                     [18 msec]
;d25: 1/(4J'(C1'N))                                    [15 msec]
;d27: 1/(8J(C1'C2'))                                   [3.2 msec]
;d29: incremented delay (F2 in 3D) = d27-d4-p19-d16-d10-p2-p22
;d30: decremented delay (F2 in 3D) = d27
;cnst21: C1' chemical shift (offset, in ppm)           [90 ppm]
;cnst23: C2' chemical shift (offset, in ppm)           [72 ppm]
;cnst30: ( cnst21 + cnst23 ) / 2
;o2p: C1'/C2' chemical shift (cnst30)
;o3p: 160 ppm
;inf1: 1/SW(N) = 2 * DW(N)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(N)) = DW(N)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in29: = in10 - in30
;in30: = d30 *2 / td1
;ns: 16 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2 : gp 3 : gp 4 : gp 5
;			  80 : 20.1 :  -40 :   60 :   -5

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1%
;gpz3: -40%
;gpz4: 60%
;gpz5: -5%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)



;$Id: na_hcncchcomq3d,v 1.10.2.1 2015/03/03 11:21:24 ber Exp $

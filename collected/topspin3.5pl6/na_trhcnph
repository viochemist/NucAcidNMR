;na_trhcnph
;avance-version (12/01/11)
;TROSY-HCN
;2D sequence with
;   inverse correlation for triple resonance
;      via TROSY and inept transfer steps
;
;      F1(H6/8) -> F2(C6/8) -> F3(N1/9,t1) 
;                    -> F2(C6/8) -> F1(H6/8,t2)
;      and/or
;      F1(H1') -> F2(C1') -> F3(N1/9,t1) 
;                    -> F2(C1') -> F1(H1',t2)
;
;phase sensitive (t1)
;only for D2O solution
;(use parameterset NA_TRHCNPH)
;
;R. Fiala, J. Czernek & V. Sklenar, 
;   J. Biomol. NMR 16, 291-302 (2000)
;(R. Riek, K. Pervushin, C. Fernandez, M. Kainosho & 
;   K. Wuethrich, J. Am. Jacs. Soc. 123, 658-664 (2001) )
;(B. Brutscher & J.-P. Simorre,
;   J. Biomol. NMR 21, 367-372 (2001) )
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"


"d0=3u"

"in0=inf1/4"


"DELTA=d0*4+p8+p2*2"
"DELTA1=d4-larger(p2,p8)/2-p19-d16"
"DELTA2=d22/2-larger(p14,p30)/2-p16-d16"


"spoff3=0"
"spoff9=0"
"spoff13=0"


1 ze
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3

3 d12 pl32:f1
  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1 pl2:f2 pl3:f3
  50u UNBLKGRAD
  
  (p1 ph1)
  p19:gp3
  d16
  DELTA1
  (center (p2 ph1) (p8:sp13 ph1):f2 )
  DELTA1
  p19:gp3
  d16
  (p1 ph2)

  p16:gp4
  d16 pl2:f2

  (p3 ph3):f2
  p16:gp5
  d16
  DELTA2
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA2
  p16:gp5
  d16 pl2:f2
  (p3 ph2):f2

  (p21 ph4):f3
  d0 
  (p2 ph1)
  d0 
  (p8:sp13 ph6):f2
  d0
  (p2 ph1)
  d0
  (p22 ph1):f3
  DELTA
  (p21 ph5):f3

  p16:gp6
  d16 pl2:f2

  (p3 ph2):f2
  p16:gp7
  d16
  DELTA2
  (center (p14:sp3 ph1):f2 (p30:sp9 ph1):f3 )
  DELTA2
  p16:gp7
  d16 pl2:f2
  (p3 ph6):f2

  p16:gp8
  d16

  (p1 ph1)
  p19:gp9
  d16
  DELTA1
  (center (p2 ph1) (p14:sp3 ph1):f2 )
  DELTA1
  p19:gp9
  d16 pl12:f2 pl16:f3

  4u BLKGRAD
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(calph(ph4, +90), caldel(d0, +in0))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 2 2
ph5=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph6=0 0 0 0 2 2 2 2
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;sp3 : f2 channel - shaped pulse 180 degree (C6/8 or/and C1')
;sp9 : f3 channel - shaped pulse 180 degree (N1/9 on resonance)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: gradient pulse 2                                 [500 usec]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p30: f3 channel - 180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay: 1-5 * T1
;d4 : 1/(4J(HC)) for both:                              [1.4 msec]
;                for H1':                               [1.56 msec]
;                for H8':                               [1.25 msec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d22: 1/(2J(CN)                                        [30 msec]
;cnst2: = J(CH)
;o2p: 90(C1') or 137(C6/C8) or 113.5 ppm(both)
;o3p: 157 ppm
;inf1: 1/SW(C) = 2 * DW(C)
;in0: 1/(4 * SW(C)) = (1/2) DW(C)
;nd0: 4
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI) in F1
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 3 : gp 4 : gp 5 : gp 6 : gp 7 : gp 8 : gp 9
;			   5 :   50 :   -9 :   30 :   -7 :   20 :   11

;for z-only gradients:
;gpz3: 5%
;gpz4: 50%
;gpz5: -9%
;gpz6: 30%
;gpz7: -7%
;gpz8: 20%
;gpz9: 11%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.100
;gpnam6: SMSQ10.100
;gpnam7: SMSQ10.100
;gpnam8: SMSQ10.100
;gpnam9: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;

;for p14 (sp3) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   either one pulse to cover 22.8ppm (C1' or C6/C8 in separate 
;      experiments)
;   (pulselength: 1ms at 600.13 MHz)
;   or a twofold modulated pulse to cover 8.8ppm at 90ppm (C1') 
;      and 137ppm (C6/C8) (both in one experiment)
;   (pulselength: 2.6ms at 600.13 MHz)
;for p30 (sp9) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;   to cover 81ppm (N1/N9)
;   (pulselength: 700us at 600.13 MHz)


;$Id: na_trhcnph,v 1.8 2012/01/31 17:49:27 ber Exp $

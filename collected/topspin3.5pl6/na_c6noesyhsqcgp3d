;na_c6noesyhsqcgp3d
;avance-version (12/08/21)
;NOESY-HSQC
;3D sequence with
;   homonuclear correlation via dipolar coupling 
;   dipolar coupling may be due to noe or chemical exchange.
;   HC correlation via double inept transfer
;with refocussing of J(C5C6) in t1
;phase sensitive (t1)
;phase sensitive (t2)
;using trim pulses in inept transfer
;water suppression using watergate sequence
;(use parameterset NA_C6NOESYHSQCGP3D)
;
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"

#   ifdef LABEL_CN
"p22=p21*2"
#   else
#   endif /*LABEL_CN*/

"p29=300u"


"cnst0=bf1*(cnst16)-o1*1000000"
"d19=1s/(4*cnst0)"


"d0=3u"
"d10=3u"

"in0=inf1/2"
"in10=inf2/2"


"DELTA2=d4-larger(p2,p8)/2"
"DELTA3=d4-p29-d16-p27*2.385-d19*5"
"DELTA4=d4-p29-d16-p27*2.154-p0*0.231-d19*5-4u"

#   ifdef LABEL_CN
"DELTA=d0*2+larger(p8,p22)"
"DELTA1=d10*2+larger(p14,p22)-p14-4u"
#   else
"DELTA=d0*2+p8"
"DELTA1=d10*2+larger(p2,p14)-p14-4u"
#   endif /*LABEL_CN*/


"TAU=d8-p16-d16"


"spoff3=0"
"spoff5=bf2*(cnst28/1000000)-o2"
"spoff13=0"


aqseq 321


1 ze
  d11 pl12:f2
2 d11 do:f2
3 d12 pl32:f1 pl0:f2
  d1 ;cw:f1 ph29
  4u ;do:f1
  d12 pl1:f1

  (p1 ph7)
  DELTA
  (p2 ph8)
  d0

#   ifdef LABEL_CN
  (center (p8:sp13 ph1):f2 (p22 ph1):f3 )
#   else
  (p8:sp13 ph1):f2
#   endif /*LABEL_CN*/

  d0
  (p1 ph1):f1

  TAU UNBLKGRAD
  p16:gp3
  d16

  (p1 ph1):f1
  DELTA2 pl0:f2
  4u
  (center (p2 ph1) (p8:sp13 ph6):f2 )
  4u
  DELTA2 pl2:f2
  p28 ph1
  4u
  (p1 ph2) 

  p16:gp4
  d16

  (p3 ph3):f2
  d10 

#   ifdef LABEL_CN
  (center (p2 ph5) (p14:sp5 ph1):f2 (p22 ph1):f3 )
#   else
  (center (p2 ph5) (p14:sp5 ph1):f2 )
#   endif /*LABEL_CN*/

  d10
  4u
  (p14:sp3 ph10):f2
  DELTA1
  4u
  (p14:sp5 ph1):f2
  2u
  2u pl2:f2
  (p3 ph4):f2

  (p1 ph1) 
  p29:gp5
  d16
  DELTA3 pl18:f1
  (center
   (p27*0.231 ph1 d19*2 p27*0.692 ph1 d19*2 p27*1.462 ph1 d19*2 p27*1.462 ph9 d19*2 p27*0.692 ph9 d19*2 p0*0.231 ph9) 
   (p8:sp13 ph1):f2 
  )
  DELTA4
  p29:gp5
  d16 pl12:f2

  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(calph(ph7, +90) & calph(ph8, +90) & calph(ph29, +90), caldel(d0, +in0)) 
     F2PH(calph(ph3, +90), caldel(d10, +in10))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph5=0 0 2 2
ph6=0
ph7=0 0 0 0 2 2 2 2
ph8=1 1 1 1 3 3 3 3
ph9=2
ph10=0 0 1 1
ph29=0
ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl18: f1 channel - power level for 3-9-19-pulse (watergate)
;pl32: f1 channel - power level for low power presaturation
;sp3 : f2 channel - shaped pulse 180 degree (on resonance)
;sp5 : f2 channel - shaped pulse 180 degree (C5 off resonance)
;sp13: f2 channel - shaped pulse 180 degree
;p0 : f1 channel -  90 degree pulse at pl18
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8: f2 channel - 180 degree shaped pulse for inversion
;p14: f2 channel - 180 degree shaped pulse
;p16: homospoil/gradient pulse
;p22: f3 channel - 180 degree high power pulse
;p27: f1 channel -  90 degree pulse at pl18
;p28: f1 channel - trim pulse
;p29: gradient pulse 3                                 [300 usec]
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J(CH))
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression, max. on H6/8
;cnst0: cnst16 - o1
;cnst2: = J(CH)
;cnst16: H6/8 chemical shift (offset, in ppm)
;cnst22: C6(CU)/C8(AG) chemical shift (offset, in ppm) [137 ppm]
;cnst28: C5 chemical shift (offset, in ppm)            [105 ppm]
;o2p: C6/8 chemical shift (cnst22)
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;ns: 8 * n
;ds: >= 16
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:    gp 3 : gp 4 : gp 5
;                        -60 :  -50 :   51

;for z-only gradients:
;gpz3: -60%
;gpz4: -50%
;gpz5: 51%

;use gradient files:   
;gpnam3: SMSQ10.100
;gpnam4: SMSQ10.100
;gpnam5: SMSQ10.50


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping



                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end


;calculate pulselength according to:
;
;   (DeltaOmega * DeltaT) / (width of region[ppm] * SFOn{MHz})
;
;for p14 (sp3, sp5) use Q3 pulse (DeltaOmega * DeltaT = 3.448)
;      to cover 22.8ppm on resonance
;      (pulselength: 1.0ms at 600.13 MHz)


;$Id: na_c6noesyhsqcgp3d,v 1.9 2012/10/08 13:04:38 ber Exp $

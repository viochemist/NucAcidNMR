;na_hpccoetf3gp3d
;avance-version (12/01/11)
;HP-COSY, HC-COSY
;3D sequence with
;   inverse correlation for triple resonance
;      via inept transfer steps
;
;      F3(P,t1) -> F1(H) -> F2(C,HMQC,t2) -> F1(H,t3)
;
;phase sensitive using Echo/Antiecho-TPPI (t1)
;phase sensitive (t2)
;(use parameterset NA_HPCCOETF3GP3D)
;
;(T. Carlomagno, M. Hennig & J.R. Williamson,
;   J. Biomol. NMR 22, 65-81 (2002) )
;(G. Varani, F. Aboul-ela, F. Allain & C.C. Gubser,
;   J. Biomol. NMR 5 315-320 (1995) )
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p22=p21*2"
"d2=1s/(cnst2*2)"
"d11=30m"
"d12=20u"
"d21=1s/(cnst4*4)"


"in0=inf1/2"
"in10=inf2/2"

"d0=3u"
"d10=in10/2-larger(p2,p22)/2-p3*2/3.1416"


"DELTA1=d21-p16-larger(p2,p8)-d0*2"
"DELTA2=d2-p16-d16-4u"


aqseq 312


1 ze
  d11 pl12:f2
2 d11 do:f2
3 d12 pl1:f1 pl2:f2 pl3:f3 

  d1
  50u UNBLKGRAD

  (p21 ph3):f3
  d0 
  (center (p2 ph5) (p8:sp13 ph1):f2 )
  d0
  p16:gp1*EA
  DELTA1 pl2:f2
  (center (p2 ph1) (p22 ph1):f3 )
  d21
  (p21 ph4):f3

  (p1 ph1)
  d2
  (p3 ph6):f2
  d10
  (center (p2 ph1) (p22 ph1):f3 )
  d10
  (p3 ph7):f2
  DELTA2
  p16:gp2
  d16 pl12:f2
  4u BLKGRAD

  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1EA(calgrad(EA), caldel(d0, +in0) & calph(ph3, +180) & calph(ph31, +180))
     F2PH(calph(ph6, +90), caldel(d10, +in10))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=1 1 1 1 1 1 1 1 3 3 3 3 3 3 3 3
ph5=0 0 2 2
ph6=0 0 0 0 2 2 2 2
ph7=0
ph29=0
ph31=0 2 0 2 2 0 2 0 2 0 2 0 0 2 0 2
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)
;spnam13: Crp60,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse                          [1 msec]
;p21: f2 channel -  90 degree high power pulse
;p22: f2 channel - 180 degree high power pulse
;d0 : incremented delay (F1 in 3D)                      [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/(2J(HC))
;d10: incremented delay (F2 in 3D) = in10/2-larger(p2,p22)/2-p3*2/3.1416
;d11: delay for disk I/O                                [30 msec]
;d12: delay for power switching                         [20 usec]
;d16: delay for homospoil/gradient recovery
;d21: 1/(4J(PH))                                        [20 msec]
;cnst2: = J(CH)
;cnst4: = J(PH)
;o2p: 77 ppm
;o3p: 0 ppm
;l4: loop for saturation sequence: = d1 / (p0 + 5m)
;inf1: 1/SW(P) = 2 * DW(P)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(P)) = DW(P)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;ns: 8 * n
;ds: >= 32
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F2
;FnMODE: echo-antiecho in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2
;			  80 : 32.4

;for z-only gradients:
;gpz1: 80%
;gpz2: 32.4%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100



;$Id: na_hpccoetf3gp3d,v 1.6 2012/01/31 17:49:27 ber Exp $

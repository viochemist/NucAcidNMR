;na_hcchfwdiecgpjr3d
;avance-version (15/02/27)
;forward directed HCC-TOCSY-CCH-E.COSY
;3D sequence with
;   inverse correlation for triple resonance using multiple
;      inept transfer steps and
;      CC-TOCSY using DIPSI3 and
;      E.COSY transfer
;
;      F1(H,t1) -> F2(Ci) -> F2(Ci+1,t2) -> F1(H,t3)
;
;phase sensitive (t1)
;phase sensitive (t2)
;with decoupling during acquisition
;water suppression using 1-1 echo pulse sequence
;(use parameterset NA_HCCHFWDIECGPJR3D)
;
;L.E. Kay, G.Y. Xu, A.U. Singer, D.R. Muhandiram & J. D. Forman-Kay
;   J. Magn. Reson. B 101, 333 - 337 (1993)
;H. Schwalbe, J.P. Marino, S.J. Glaser & C.Griesinger,
;   J. Am. Chem. Soc. 117, 7251 - 7252 (1995)
;J.P. Marino, H. Schwalbe, S.J. Glaser & C.Griesinger,
;   J. Am. Chem. Soc. 118, 4388 - 4395 (1996)
;S.J. Glaser, H. Schwalbe, J.P. Marino & C. Griesinger,
;   J. Magn. Res. B 112, 160 - 180 (1996)
;
;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


prosol relations=<triple_na>


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"p4=p3*2"
"p22=p21*2"
"d2=1s/(cnst2*2)"
"d4=1s/(cnst2*4)"
"d11=30m"
"d12=20u"
"d23=1s/(cnst3*8)"


"p16=1m"
"p19=5m"
"p29=4.4m"


"d0=3u"
"d10=3u"
"d30=d22/2"

"in0=inf1/2"
"in10=inf2/2"

"in30=in10"

"td2=tdmax(td2,d30*2,in30)"


"l1=(d15/(p9*217.30))"


"DELTA=d4-d0*2-p22-p4"
"DELTA1=d3-p16-d16"
"DELTA2=d22/2-p22-d10-8u"
"DELTA3=d23-d2-p1*2-4u"
"DELTA4=p3-4u"


aqseq 321


1 ze 
  d11 pl12:f2 pl16:f3
2 d11 do:f2 do:f3
3 d12 pl32:f1 pl2:f2 pl3:f3

  d1 cw:f1 ph29
  4u do:f1
  d12 pl1:f1

  (p1 ph3)
  d0
  (p22 ph1):f3
  DELTA
  (p4 ph1):f2
  d0 
  (p2 ph1) 
  d4 UNBLKGRAD           
  (p1 ph2) 

  p16:gp1
  d16

  (p3 ph4):f2
  p16:gp2
  d16
  DELTA1
  (center (p2 ph1) (p4 ph1):f2 )
  DELTA1
  p16:gp2
  d16 pl15:f2

  (p20 ph10):f2
						;begin DIPSI3
9 (p9*2.722 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph10):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph10):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph10):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph10):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.722 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph10):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph10):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph10):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph10):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph10):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.778 ph12):f2
  (p9*3.056 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.556 ph10):f2
  (p9*4.000 ph12):f2
  (p9*2.722 ph10):f2
  (p9*4.111 ph12):f2
  (p9*3.778 ph10):f2
  (p9*3.889 ph12):f2
  (p9*2.889 ph10):f2
  (p9*3.000 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.500 ph12):f2
  (p9*4.050 ph10):f2
  (p9*2.830 ph12):f2
  (p9*4.389 ph10):f2
  (p9*2.722 ph10):f2
  (p9*4.389 ph12):f2
  (p9*2.778 ph10):f2
  (p9*3.056 ph12):f2
  (p9*0.333 ph10):f2
  (p9*2.556 ph12):f2
  (p9*4.000 ph10):f2
  (p9*2.722 ph12):f2
  (p9*4.111 ph10):f2
  (p9*3.778 ph12):f2
  (p9*3.889 ph10):f2
  (p9*2.889 ph12):f2
  (p9*3.000 ph10):f2
  (p9*0.333 ph12):f2
  (p9*2.500 ph10):f2
  (p9*4.050 ph12):f2
  (p9*2.830 ph10):f2
  (p9*4.389 ph12):f2
  lo to 9 times l1
						;end DIPSI3
  4u
  4u pl2:f2

  d10
  (p22 ph1):f3
  DELTA2
  (p4 ph1):f2 
  d30
  (p3 ph5):f2
  d23 
  (p4 ph6):f2
  DELTA3
  (p1 ph8)
  4u
  (p1 ph9)
  d2
  (p3 ph7):f2

  p16:gp3
  d16
  (p1 ph1 d19*2 p1 ph12)

  d2 
  p16:gp3
  d16 pl12:f2 pl16:f3
  DELTA4

  4u BLKGRAD               
  go=2 ph31 cpd2:f2 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2
     F1PH(calph(ph3, +90), caldel(d0, +in0))
     F2PH(calph(ph5, +90) & calph(ph6, +90) & calph(ph7, +90), caldel(d10, +in10) & caldel(d30, -in30))
exit
  

ph1 = 0
ph2 = 1
ph3 = 0 0 0 0 2 2 2 2
ph4 = 0 2
ph5 = 0
ph6 = 0 
ph7 = 0 0 2 2
ph8 = (360) 135
ph9 = (360) 0 
ph10= 0 
ph12= 2
ph29=0
ph31= 0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;pl15: f2 channel - power level for TOCSY-spinlock
;pl16: f3 channel - power level for CPD/BB decoupling
;pl32: f1 channel - power level for low power presaturation
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p9 : f2 channel -  90 degree low power pulse
;p16: homospoil/gradient pulse 1 			[1 ms]
;p19: gradient pulse 2                                  [5 ms]
;p20: f2 channel - trim pulse                           [2 ms]
;p21: f3 channel -  90 degree high power pulse
;p22: f3 channel - 180 degree high power pulse
;p29: gradient pulse 3                                  [4.4 ms]
;d0 : incremented delay (F1 in 3D)			[3 us]
;d1 : relaxation delay; 1-5 * T1		
;d2 : 1/(2J(HC))                                        [3.1 ms]
;d3 : 1/(4J(HC)) for CH (RNA)
;     1/(8J(HC)) for CH2 (DNA)
;d4 : 1/(4J(HC))					[1.6 ms]
;d10: incremented delay (F2 in 3D)			[3 us]
;d11: delay for disk I/O                               	[30 ms]
;d12: delay for power switching                        	[20 us] 
;d15: TOCSY mixing time (CC)
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d22: constant time delay for directed transfer 
;        C1'-C4'                                   [8.3 ms]
;        C1'-C5'                                   [7.6 ms]
;d23: 1/(8J(CC))                                        [3.1 ms]
;d30: decremented delay (F2 in 3D) = d22/2
;cnst2: = J(HC)
;cnst3: = J(CC)
;o2p: 77 ppm
;o3p: 0 ppm
;l1: loop for DIPSI cycle:
;       mixing time = ((p9*217.3) * l1) + (p20)
;       for the directed transfer:
;          C1'-C4'                                 [9.2 ms]
;          C1'-C5'                                 [13.5 ms]
;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(C) = 2 * DW(C)
;in0: 1/(2 * SW(H)) =  DW(H)
;nd0: 2
;in10: 1/(2 * SW(C)) = DW(C)
;nd10: 2
;in30: = in10
;ns: 4 * n
;ds: >= 32 
;td1: number of experiments in F1
;td2: number of experiments in F2       td2 max = 2 * d30 / in30  
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;cpd3: decoupling according to sequence defined by cpdprg3
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:  	gp 1 : gp 2 : gp 3 
;			  70 :  -80 :   60  

;for z-only gradients:
;gpz1: 70%
;gpz2: -80%
;gpz3: 60%

;use gradient files:
;gpname1: SMSQ10.100
;gpname2: SMSQ10.100
;gpname3: SMSQ10.100


;set pl32 to 0W when presaturation is not required
;   use pl1 + 75 to 80dB to reduce radiation damping



;$Id: na_hcchfwdiecgpjr3d,v 1.8.2.1 2015/03/03 11:21:24 ber Exp $

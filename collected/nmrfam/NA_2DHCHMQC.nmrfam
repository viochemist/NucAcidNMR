;NA_2DHCHMQC.nmrfam
;
; 2D 1H-13C HMQC correlation via heteronuclear zero and double quantum coherence
; Phase sensitive using States-TPPI and with decoupling during aquisition

; Bruker Avance version for Topspin 1.3 or XWIN-NMR 3.5
; Written by L. Clos, NMRFAM (lclos@nmrfam.wisc.edu)
;
;  Disclaimer: This pulse program is provided "as is" for your
;  information. Support for the use of this pulse program is
;  provided to users of the National Magnetic Resonance Facility
;  at Madison (NMRFAM). Users of this pulse program employ it at
;  their own risk. Neither NMRFAM nor University of Wisconsin-Madison
;  are liable for any physical or other damage incurred during the
;  use of this pulse program.

#include <Avance.incl>
#include <Nucs.incl>

"d0=(in0/2)-(p1*4/3.14159)"
"d11=30m"
"d12=20u"
"d13=3u"
"d29=d1-p9-d12"
"d2=(1s/(2*cnst2))-(0.1*(1/(2*cnst2)))"

"p2=p1*2"

;Pulse sequence starts here
1 ze
2 d11 do:C pl9:H        ;end C decoupling, set H presaturation power
  d29                   ;recycle delay
  (p9*0.8 ph0):H        ;80% presaturation pulse
  d13 ip0               ;increment presat phase
  (p9*0.2 ph0):H        ;20% presaturation pulse
  d12 pl1:H             ;set H hi power
  (p1 ph1):H            ;H hard 90 ,creates in-phase proton SQC
  d2 pl2:C              ;set C hi power, evolution of antiphase proton SQC
  (p3 ph11):C           ;C hard 90, creates ZQC and DQC of 1H-13C
  d0                    ;1/2 incremented delay for DQC evolution
  (p2 ph2):H            ;H hard 180, refocuses 1H chemical shift by swapping ZQC and DQC
  d0                    ;1/2 incremented delay
  (p3 ph12):C           ;C hard 90, converts 1H-13C DQC into 1H antiphase SQC
  d2 pl12:C             ;set decoupling power, evolution to inphase 1H SQC
  go=2 ph31 cpd2:C      ;record and loop experiment ns times, decouple 13C
  d11 do:C mc #0 to 2 F1PH(ip11 & rp0, id0)
exit

ph0= 0
ph1= 0
ph2= 0
ph11=0 2
ph12=0 0 2 2
ph31=0 2 2 0

;pl1 : H channel high power level for 90 pulse              [-6 dB]
;pl2 : C channel high power level for 90 pulse              [-6 dB]
;pl9 : H channel low power level for presat                 [~70 dB]
;pl12: C channel decoupling power                           [~16 dB]
;p1  : H channel high power 90 pulse                        [~11 u]
;p2  : H channel high power 180 pulse                       [~22 u]
;p3  : C channel high power 90 pulse                        [~16 u]
;p9  : f1 channel - low power presat pulse                  [~0.5 s]
;pcpd2: C channel decoupling pulse length                   [~120 u]
;d0  : incremented delay (2D) =(in0/2)-(p1*4/3.14159)
;      calculated for easy F1 phasing(PHC0=90,PHC1=-180)
;d1  : relaxation delay; 1-5 * T1                           [>1 s]
;d2  : 1/(2*JCH)=coupling time
;d11 : delay for disk r/w                                   [30 m]
;d12 : power switching delay                                [20 u]
;d13 : short delay                                          [3 u]
;d29 : alt relax delay when presat is used = (d1-p9)
;cnst2: JCH in Hz                        [160 for Ribose 195 for Base]
;in0 : 1/(1 * SW) = 2*DW
;nd0 : 2
;NS  : 4 * n
;DS  : 16
;td1 : number of experiments
;mc2 : States-TPPI, TPPI

;$Id: NA_2DHCHMQC.nmrfam,v 1.0 2009/06/06 16:20:00 lclos Exp lclos $
;FNORD 42


  

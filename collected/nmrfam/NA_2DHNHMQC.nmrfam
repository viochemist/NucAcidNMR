;NA_HNHMQC.nmrfam
;
; 2D 1H-15N HMQC correlation via heteronuclear zero and double quantum coherence
; Phase sensitive using States-TPPI and with decoupling during aquisition
; 1-1 echo and gradient solvent suppression
;
; Bruker Avance version for Topspin 1.3 or XWIN-NMR 3.5
; Written by L. Clos, NMRFAM (lclos@nmrfam.wisc.edu)
;
;  Disclaimer: This pulse program is provided "as is" for your
;  information. Support for the use of this pulse program is
;  provided to users of the National Magnetic Resonance Facility
;  at Madison (NMRFAM). Users of this pulse program employ it at
;  their own risk. Neither NMRFAM nor University of Wisconsin-Madison
;  are liable for any physical or other damage incurred during the
;  use of this pulse program.

#include <Avance.incl>
#include <Grad.incl>
#include <Nucs.incl>

define delay TAUW
define delay DELTA
define delay DIFFW
define delay NULL2MAX

"d0=(in0/2)-(p1*4/3.14159)"
"d11=30m"
"d12=20u"
"d13=3u"
"d16=200u"
"d29=d1-p9-d12"
"d4=(1s/(4*cnst2))-(0.1*(1/(4*cnst2)))"
"TAUW=(1s/(NULL2MAX*4))-(p1*0.667)"
"DIFFW=(TAUW-p4/2)"
"DELTA=d4-p16-d16-d12"

"p4=p3*2"

;Pulse sequence starts here
1 ze
  d11 pl1:f1            ;set H hi power
2 d11 do:N do:C         ;end decoupling
  d1 pl2:C pl3:N        ;recycle delay, set C and N hi power
  d11 UNBLKGRAD         ;clear gradients
  (p1 ph1):H            ;H hard 90
  TAUW                  ;Water suppresion delay
  (p1 ph2):H            ;H hard 90 for H2O to +z
  DELTA                 ;1/4J minus gradient
  d12
  p19:gp1               ;gradient to dephase max signals
  d16                   ;gradient recovery
  (p5 ph21):N           ;N hard 90
  d0                    ;1/2 incremental delay
  (p1 ph3):H            ;H hard 90
  DIFFW                 ;Water suppression delay minus C 180
  (p4 ph11):C           ;C hard 180
  DIFFW                 ;Water suppression delay minus C 180
  (p1 ph4):H            ;H hard 90
  d0                    ;1/2 incremental delay
  (p5 ph22):N           ;N hard 90
  DELTA pl12:C pl13:N   ;1/4J minus gradient, set decoupling powers
  p19:gp1               ;gradient to refocus max signals
  d16                   ;gradient recovery
  d12 BLKGRAD           ;block gradients
  go=2 ph31 cpd3:N      ;record and loop NS times, decouple C
  d11 do:N mc #0 to 2 F1PH(ip21, id0)
exit

ph1 = 0
ph2 = 2
ph3 = 0 0 0 0 1 1 1 1 2 2 2 2 3 3 3 3
ph4 = 2 2 2 2 3 3 3 3 0 0 0 0 1 1 1 1
ph11= 2
ph21= 0 2
ph22= 0 0 2 2
ph31= 0 2 2 0 2 0 0 2

;pl1 : H channel high power level for 90 pulse              [-6 dB]
;pl2 : C channel high power level for 90 pulse              [-6 dB]
;pl3 : N channel high power level for 90 pulse              [-6 dB]
;pl12: C channel decoupling power                           [~16 dB]
;pl13: N channel decoupling power                           [~6.5 dB]
;p1  : H channel high power 90 pulse                        [~11 u]
;p3  : C channel high power 90 pulse                        [~16 u]
;p4  : C channel high power 180 pulse                       [~32 u]
;p5  : N channel high power 90 pulse                        [~40 u]
;p19 : water suppression gradient length                    [800 u]
;pcpd2: C channel decoupling pulse length                   [~120 u]
;pcpd3: N channel decoupling pulse length                   [~200 u]
;d0  : incremented delay (2D) =(in0/2)-(p1*4/3.14159)
;      calculated for easy F1 phasing(PHC0=90,PHC1=-180)
;d1  : relaxation delay; 1-5 * T1                           [>1 s]
;d4  : 1/(4*JCH)=coupling time
;d11 : delay for disk r/w                                   [30 m]
;d12 : power switching delay                                [20 u]
;d13 : short delay                                          [3 u]
;d16 : delay for homospoil/gradient recovery                [200 usec]
;TAUW: jump-return precession delay = 1/(4*NULL2MAX)
;NULL2MAX: null to max signal distance in Hz                [10*bf1]
;           = (distance from HDO to iminos)
;DIFFW: TAUW-C90
;DELTA: 1/4JNH-gradient length
;cnst2: JNH in Hz                                           [90]
;in0 : 1/(1 * SW) = 2*DW
;nd0 : 2
;NS  : 4 * n
;DS  : 16
;td1 : number of experiments
;mc2 : States-TPPI, TPPI

;gpz1: water suppresion gradient (100% = 50g/cm)            [61%]
;gpnam1: shape of gradient                                  [sine.100]

;$Id: NA_2DHNHMQC.nmrfam,v 1.0 2009/06/06 16:20:00 lclos Exp lclos $
;FNORD 42
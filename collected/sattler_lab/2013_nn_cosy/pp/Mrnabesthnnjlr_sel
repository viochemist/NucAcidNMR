;Mrnabesthnnjlr_sel
;Best version for use in H2O
; H,N F3 long-range H-N-N for RNA 
; with 13C decoupling
; in H2O


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

"in0=inf1/2"
;"d1=300m"
"d16=200u"
"p16=1m"
"p2=p1*2"
"l0=td1/2"
"cnst9=7.5" ;offset for F1 centered on H2 region
"cnst19=228" ;offset for F3 centered on N1/N7 of A
"cnst21=0.529" ;compensation of chemical shift evolution during p21 Pc9_4_90.1000
"cnst22=0.5"
"cnst23=0.529"
"spoff19=bf3*(cnst19/1000000)-o3"
"spoff21=bf1*(cnst9/1000000)-o1"
"spoff22=bf1*(cnst9/1000000)-o1"
"spoff23=bf1*(cnst9/1000000)-o1"


;"d4=17.0m/2"		; long range H-(C)-N transfer
;"d25=50m/2"		; for J(N,N), 30-45ms
"DELTA1=d4-d16-p16-p21*cnst21";-p22*cnst22"
"DELTA2=d4-d16-p16-p23*cnst23";-p22*cnst22"
"DELTA3=d4-d16-p16";-p22*cnst22"
"DELTA4=d25/2-p29/2-p5*2/3.1416"
"DELTA5=d25-p29"

"cnst10=13.5"
"spoff24=bf1*(cnst10/1000000)-o1"
"d0=(in0)-(p5*4/3.14+p4)/2"       ;-180/360



1 ze
2 d1 do:f3 do:f2
  15m
3 6m
4 20u pl0:f3 
  20u pl12:f2
  20u pl0:f1
  (p21:sp21 ph0):f1
  DELTA1
  p16:gp0
  d16
  3u
  (center (p19:sp19 ph19):f3 (p22:sp22 ph0):f1)
  3u
  d16 pl3:f3
  p16:gp0
  DELTA1
  (p21:sp21 ph2):f1
  50u UNBLKGRAD
  p16:gp1
  d16 
  (p5 ph3):f3          ; ----------------
  DELTA4 pl0:f3
  3u
  (p29:sp29 ph19):f3
  DELTA5
  (p29:sp29 ph19):f3
  3u
  DELTA4 pl3:f3 pl2:f2 
  (p5 ph13):f3          ; ----------------

  if "d0 > p24"
  {
  (center (d0 p4 ph0 d0):f2 (p24:sp24 ph0):f1)
  }
  else
  {
  (d0 p4 ph0 d0):f2
  }

  (p5 ph4):f3           ; --------------- N-->N
  DELTA4 pl0:f3
  3u
  (p29:sp29 ph19):f3
  DELTA5
  (p29:sp29 ph19):f3
  3u
  DELTA4 pl3:f3
  (p5 ph0):f3           ; ----------------
  p16:gp2
  d16
  (p23:sp23 ph22):f1
  DELTA2
  p16:gp3
  d16 pl0:f3
  3u
  (center (p19:sp19 ph11):f3 (p22:sp22 ph0):f1)
  3u
  d16 pl12:f2 pl13:f3
  p16:gp3
  DELTA3 BLKGRAD
  go=2 ph31 cpds2:f2 cpd3:f3 
  d1 do:f2 do:f3 wr #0 if #0 zd
  5m ip3
  5m ip13
  5m ip19
  lo to 3 times 2
  10m id0
  lo to 4 times l0
exit


ph0=0
ph2=1
ph3=0 2
ph11=1
ph13=1 1 3 3
ph4=1
ph12=0
ph19=0
ph22=0
ph31=0 2

;gpz0 -60
;gpz1 40
;gpz2 70
;gpz3 -50
;d1 ~300m as optimum
;d4 ~9.5m as optimum including compensation for chemical shift evolution during Reburp pulses
;d25 ~17.5m for long (70nt) and ~22.5 for short (25nt) RNA
;p16 1000u spoil gradient
;d16 200u gradient recovery
;p1 1H hard pulse (for referencing shapes)
;p5 15N hard pulse
;p19 4.8m@600 
;spnam19 Reburp.1000 for selective refocussing of N1/N7 and not N3
;p21 6.0m@600 
;spnam21 Pc94_90.1000 (about 1200Hz bandwidth)
;p22 4.8m@600 
;spnam22 Reburp.1000 (about 1200 bandwidth)
;p23 6.0m@600 
;spnam23 Pc9_4_90.1000 (about 1200Hz bandwidth)
;p24 1.0m@600 
;spnam24 Reburp.1000 (about 7200Hz bandwidth) centered on iminos @13.5 ppm
;p29 4.0m@600 
;spnam29 rna_phs8,0.7,4m.1 adiabatic refocussing for selective refocussing
;O2P set to 154 ppm
;O3P set to 188 ppm
